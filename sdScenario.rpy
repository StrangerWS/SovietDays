# ================================================================================== #
#                   ОФИЦИАЛЬНАЯ ДОКУМЕНТАЦИЯ ФАЙЛА СЦЕНАРИЯ                          #
# ================================================================================== #
# Файл сценария мода «Советские будни».                                              #
# Разработчик — StrangerWS, 2015-2016.                                               #
# ВСЕ ДАННЫЕ, ИСПОЛЬЗОВАННЫЕ В ЭТОМ МОДЕ, ПРИНАДЛЕЖАТ ИХ ПЕРВИЧНЫМ ПРАВООБЛАДАТЕЛЯМ! #
#    АВТОР МОДА НЕ НЕСЁТ ОТВЕТСТВЕННОСТИ ЗА ДАЛЬНЕЙШЕЕ РАСПРОСТРАНЕНИЕ МАТЕРИАЛОВ!   #
# ================================================================================== #

init:
    $ filters["sdPlayMod"] = u"(Альфа) Советские Будни"
    
    # Инициализируем все данные, с которыми будем работать.
    
    call initVars       # Переменные мода
    call initChars      # Персонажи
    call initImg        # Картинки
    call initSnd        # Звуки, музыка
    call initMiniMeow   # Переменные мини-игры
    
# Для тестеров!   
label sdTests:
    stop music fadeout 2
    hide screen sdMenu 
    with fade
    scene bg black
    
    $ set_mode_nvl()
    window show
    
    $ persistent.sprite_time = "prolog"
    $ prolog_time()
    
    menu:
    
        "Первый день. ЮВАО-кусок.":
        
            $ set_mode_adv()
            $ persistent.sprite_time = "night"
            $ night_time()
            jump sdUvao_1day
            
        "ЮВАО-рут":
        
            $ set_mode_adv()
            $ persistent.sprite_time = "day"
            $ day_time()
            jump sdUvRoute
            
        "Мини-игры: Кис-мяу":
            
            $ set_mode_adv()
            $ persistent.sprite_time = "day"
            $ day_time()
            scene ext_square_day with dissolve
            show us smile pioneer with dissolve
            jump sdMiniMeowSasha
            
        "Тест Меню виджетов":
        
            jump sdWidgetMenu
            
        
        
        "Выход в главное меню мода":
        
            window hide
            show screen sdMenu
            with fade2
            play music five_years fadein 3
            $ renpy.pause(hard = True)
        
label sdPrepare:
 
# Инициализация переменных, которые должны проинициализироваться только один раз.
    call initMap
    call initRnd
    
    jump sdIntro
            
label sdIntro:
    stop music fadeout 2
    hide screen sdMenu 
    with fade
    
    $ set_mode_adv()
    $ sdStart = True
    $ sd_chapter(1, u"Предыстория.")
    $ persistent.sprite_time = "prolog"
    $ prolog_time()
    play music music_list["a_promise_from_distant_days_v2"] fadein 3
    scene black
    $ renpy.pause(3, hard = True)
    scene anim prolog_1 with fade3
    
    "Советское время... {w}Беззаботное и весёлое."
    "В самые плохие дни я вспоминаю то время. {w}Восьмидесятые годы."
    "Годы моей юности."
    "В это время я и стал тем, кто я есть. {w}В это время я встретил свою первую - и единственную - любовь."
    
    scene bg ext_city_day with dissolve
    
    "Я родился в маленьком городке, настолько неизвестном, что время стёрло его название с карт СССР."
    "Да и не имеет это никакого значения."
    "В семье нас было двое - я и моя младшая сестра, Женя."
    "Мы были обычными советскими детьми, все, как один, мечтали стать пионерами и космотавтами."
    "Мой школьный друг Сергей, взявший себе кличку «Электроник», всё детство провёл вместе с нами."
    "Мы сидели с ним за одной партой, порой, прогуливали занятия, чтобы попинать мячик во дворе."
    "В нашей семье Электроника принимали как родного.{w} Его ждали всегда - и на Новый год, и на масленицу, и на первомай - в общем, место за нашим столом для него всегда было."
    
    stop music fadeout 4
    
    "Шли годы, мы взрослели..."
    
    scene bg int_classroom_day with dissolve
    play music music_list["everyday_theme"]
    
    "Шёл последний год учёбы. Близились летние каникулы, а в школе нам сказали про возможность поехать в пионерлагерь."
    "Учитель выложил список всех лагерей на стол, и все дети моментально выстроились в огромную колонну, ожидая своей очереди."
    "Я в лагерях никогда не был - скучно это, если одному ехать, а за Женей следить надо - я же, хоть и на год, но старше."
    "Первым из толпы вынырнул Электроник."
    
    show el smile pioneer with dissolve
    
    el "Сань, а ты чего? В лагерь не хочешь?"
    sdme "Да хочу... Только не решил куда."
    
    show el surprise pioneer with dspr
    
    el "А чего тогда тут сидишь? Иди, записывайся, там на месте решишь."
    "Я заметил, что у него промелькнула какая-то мысль."
    
    show el smile pioneer with dspr
    
    el "Слушай, а поехали со мной в Совёнок! И Женьку возьми, что дома тухнуть будет?"
    th "А что, вместе же веселее... Может, съездить?"
    sdme "А давай!"
    
    hide el with dissolve
    
    "Я пробился сквозь толпу и крикнул:"
    sdme "Анатолий Михайлович, места в Совёнок есть?"
    "Учитель кивнул мне."
    sdme "Запишите два места!"
    
    scene bg ext_city_day with dissolve
    stop music fadeout 4
    
    "Дома я рассказал родителям про эту затею."
    "Они приняли её довольно тепло и даже порадовались, что мы с сестрой познаем все прелести пионерлагеря."
    "Дни ожидания прошли быстро - Электроник не дал мне заскучать - и мы уже собираем вещи."
    
    play music music_list["my_daily_life"]
    show el smile pioneer at left
    show mz normal glasses pioneer at fright 
    with dissolve
    
    "На пороге дома ждал верный друг."
    el "Здор{b}о{/b}во, Сань! {w}О, и тебе привет, мелкая."
    
    show mz angry glasses pioneer with dspr
    
    mz "Сам ты мелкий!"
    el "Я чего пришёл... Я к тебе в автобусе подсяду, у меня тут знакомый объявился, с кружка кибернетиков."
    sdme "Это который Шурик, про которого ты мне все уши прожужжал?"
    "Электроник кивнул."
    
    show el serious pioneer with dspr
    
    el "Ты увидишь, этот парень - просто гений!{w} Он, кстати, тоже в Совёнок."
    sdme "Значит, скучать не придётся."
    
    show el grin pioneer with dspr
    
    el "Так точно! Ну, я пошёл."
    
    hide el with dissolve
    show mz normal glasses pioneer close at center with ease
    
    mz "А мы с мамой за продуктами пойдём. Смотри, не уезжай без меня!"
    
    show mz laugh glasses pioneer with dspr
    
    mz "А то голодный останешься!"
    
    show mz laugh glasses pioneer far with dspr
    
    sdme "Да куда я без тебя уеду!"
    
    hide mz with dspr
    
    th "Мне ж потом башку оторвут."
    
    stop music fadeout 4
    
    "Сестра убежала к маме, а я пошёл к автобусу с вещами."
    
    jump sdBus

label sdBus:
    $ sdTimeskip()
    $ persistent.sprite_time = "day"
    $ sd_chapter(1, u"Автобус.")
    $ day_time()
    scene bg ext_bus1 with sdTransition
    play music music_list["smooth_machine"]
    play ambience ambience_medium_crowd_outdoors
    
    "Я подошёл к автобусу, в который загружались ребята."
    "Девушка лет двадцати пяти - видимо, вожатая - пересчитывала всех, помечая что-то у себя в планшете."
    "Я нерешительно перевалился с ноги на ногу и сделал шаг."
    "Она заметила меня и растянулась в приветственной улыбке."
    
    show mt grin panama pioneer far at fright with dissolve
    
    mt "Молодой человек, вы к нам?"
    sdme "В Совёнок? Да, к вам, наверное."
    
    show mt smile panama pioneer far at fright with dspr
    
    mt "Тогда добро пожаловать! Я - Ольга Дмитриевна, вожатая. А ты кто, собственно?"
    sdme "Александр."
    mt "О! В списке только один Александр, и он... {w}Как раз в моей группе.{w} Ну что ж, располагайся, ехать долго."
    
    hide mt with dissolve
    
    "Я начал заталкивать свой рюкзак в багажный отсек Икаруса, как вдруг вожатая поинтересовалась:"
    
    show mt smile panama pioneer at fright with dissolve
    
    mt "Саш, я бы хотела попросить тебя о помощи. Ты же примерный пионер?"
    th "Ещё не успели приехать в лагерь, а меня уже заставляют в чём-то участвовать."
    th "Да, я определённо отдохну."
    sdme "Да, всегда рад помочь. В чём суть?"
    show mt normal panama pioneer at center with ease
    
    mt "Понимаешь, вас, пионеров, много. За всем не уследишь. {w}Я, конечно, нашла себе помощницу, но объём работы у нас большой."
    mt "Ты не мог бы разделить с нами эту долю?"
    
    menu:
        "Согласиться":
        
            $ mtmerc += 1
            $ sdChangeImpression(5)
            
            sdme "Вообще, почему бы и нет. Я в деле, обращайтесь."
            
            show mt laugh panama pioneer with dspr
            
            mt "Ну вот, сразу видно - образцовый пионер! Молодец!"
            
        "Отказаться":
            $ mt_denied_1day = True
            $ sdChangeMood(-10)
            $ sdChangeImpression(-5)
            
            sdme "Знаете, Ольга Дмитриевна, боюсь, я не справлюсь с такой ответственностью, возложенной на меня."
            
            show mt sad panama pioneer with dspr
            
            mt "Ну что ж, тогда ладно."
            sdme "Не поймите меня неправильно, просто со мной всегда случается что-то плохое. Человек-неудача просто."
            
            show mt normal panama pioneer with dspr
            
            mt "Нет, Саш, ничего страшного. Я понимаю."
    
    
    "Я вдруг вспомнил про сестру."
    th "Как бы ей подсолить..."
    th "О! Придумал - пусть поработает."
    sdme "Ольга Дмитриевна, со мной ещё сестра едет, Женя. Она тоже у вас в списке?"
    
    show mt normal panama pioneer with dspr
    
    mt "Женя, Женя... Да, есть такая."
    sdme "Вы не могли бы её в библиотеку записать? Она {i}очень{/i} любит читать."
    
    show mt surprise panama pioneer with dspr
    
    "Слово «очень» я настолько сильно протянул, что вожатая посмотрела на меня исподлобья."
    mt "Правда?"
    sdme "Правда-правда. Она сама попросила, опаздывает немного."
    
    show mt smile panama pioneer with dspr
    
    mt "Ну ладно, раз ты так заботишься о сестре..."
    
    $ sdChangeImpression(5)
    
    th "Да, забочусь..."
    "Я злобно усмехнулся про себя."
    
    stop music fadeout 4
    
    hide mt with dspr
    window hide
    show blinking
    if mtmerc == 1:
        $ slspotted = True
        window show
        
        "..."       
        "Я снова принялся за погрузку своих и Жениных вещей в автобус."
        
        play music music_list["two_glasses_of_melancholy"]
        show sl normal pioneer far at fleft with dissolve
        
        "Стоило мне закончить с этим, как из-за угла вывернула девушка."
        "С собой она тащила чемодан, который явно был тяжёлым."
        
        show mt smile panama pioneer with dissolve
        
        mt "А вот и Славяна подошла. Познакомься с напарницей. А я спешу, у меня ещё куча дел."
        
        hide mt with dspr
        show sl normal pioneer with dissolve
        
        "Я немного ошалел и растерял все свои манеры вместе с даром речи."
        "У моей «напарницы» была довольно приятная внешность.{w} Если не сказать больше - таких красивых девушек я ещё не встречал."
        
        show sl shy pioneer at center with ease
            
        "Славяна смущённо улыбнулась. Видимо, девушка не привыкла к такому вниманию со стороны парней."
        th "Молодец, парень. Теперь ты должен жениться на ней. Или хоть что-то сказать."
        
        menu:
        
            "Проявить активность":
                $ slmet = True
                
                sdme "Давай помогу с чемоданом. Я всё равно тут временный грузчик."
                
                $ ptssl += mulsl * 2
                show sl smile pioneer with dspr
                
                "«Напарница» вновь улыбнулась, в этот раз бодрее."
                sl "А ты тоже из нашей смены?"
                sdme "Вроде как. Я Саша."
                
                play sound sfx_drop_alisa_bag
                
                "Сказал я, закидывая её чемодан в автобус."
                sl "А я - Славя. Вообще, полное имя - Славяна, но все меня Славей зовут."
                sdme "Ну, раз все зовут, то и я буду."
                "Улыбнулся я."
                th "Приятная, всё-таки, девочка. Хоть и неловко так поначалу вышло."
                sdme "Мы, вроде как, партнёры."
                
                show sl surprise pioneer with dspr
                
                sl "В каком смысле?"
                sdme "Вожатая наша, Ольга Дмитриевна, попросила помочь вам с организацией порядка в лагере."
                "Я вытянулся по струнке и поднял руку над головой."
                
                show sl laugh pioneer with dspr
                
                sl "Да, помощь нам не помешает. Спасибо!"
                
                show sl normal pioneer with dspr
                
                sdme "Пока что не за что благодарить. Я ещё ничего толком и не сделал."
                
                show sl smile pioneer with dspr
                
                sl "Ну, почему же - ты помог бедной девушке дотащить её вещи до автобуса. Это уже плюс!"
                sl "Ладно, увидимся в лагере!"
                "Она улыбнулась мне на прощание и скрылась в бездне внутренностей железного исполина."
                
            "Неловко молчать":
                $ sdChangeMood(-5)
                
                "Я попытался было вытянуть из себя хоть слово, но быстро понял, что выйдет из этого только приглушённое мычание."
                th "Засмущал девушку, хорошее начало дня."
                
                show sl smile pioneer with dspr
                play sound sfx_drop_alisa_bag
                
                "Девушка, тем временем, сама закинула свои вещи в автобус, снова улыбнулась мне, на этот раз дружелюбно, и скрылась в салоне."
                "Я лишь тряхнул головой, чтобы освободиться от неловкости, и присел на лавку неподалёку."

        hide sl with dissolve 
        stop music fadeout 4

        "Через какое-то время вдалеке показался Электроник."
        
        show el normal pioneer with dissolve 
        
        el "Шурик потом подойдёт, но я вас обязательно познакомлю!"
        sdme "Ну, хорошо."
        
    else:
        "Вожатая, видимо, расстроилась и куда-то ушла, оставив меня наедине с неприятным осадком от отказа."
        "Однако, долго мне скучать не пришлось."
        "Вдалеке я заметил двух приближающихся пионеров."
        "Одного я узнал сразу - это был Электроник. Второй, судя по всему - Шурик, о котором я был наслышан от друга."
        
        show el normal pioneer at cleft
        show sh normal pioneer at cright 
        with dissolve
        
        el "Физкульт-привет! Ну, знакомься, Сань, это Шурик."
        "Я кивнул головой и протянул руку."
        el "Шурик, это Саша, мой лучший друг и товарищ."
        
        show sh serious pioneer with dspr
        
        sh "А как, товарищ, ты относишься к кибернетике?"
        sdme "Весьма положительно."
        
        show sh normal_smile pioneer with dspr
        
        sh "То есть, мы можем рассчитывать на твою поддержку в лагере?"
        sdme "Конечно! Ради развития советской кибернетики - всё, что угодно!"
        "Я, похоже, переигрывал, но, в целом, был не против поучаствовать в их экспериментах."
        
        show sh smile pioneer with dspr
        
        sh "Отлично! Тогда, до встречи в лагере!"
        
        hide sh with dissolve
        show el smile pioneer at center with ease
        
        "И он быстро скрылся внутри автобуса."
        "Электроник одобрительно кивнул."
        
        show el serious pioneer with dspr
        
        el "Ты познаешь все прелести кибернетики, товарищ!"
        
        show el laugh pioneer with dspr
        
        "Мы рассмеялись."
        
    show blink
    $ renpy.pause(1)
    show el normal pioneer at sdSitLeft
    hide blink
    show unblink
            
    "Мы сидели на лавке недалеко от автобуса, мечтая о том, каким будет лагерь, как вдруг Электроник толкнул меня в бок."
    el "Смотри!"
    
    show mi normal pioneer far at fright with dissolve 
    play music music_list["so_good_to_be_careless"]
    
    "К нам подходила девушка, несущая за спиной гитару и со всех сторон обвешанная сумками."
    
    if slspotted == True:
    
        th "Уже вторая девушка, от которой я теряю дар речи. Превосходно!"
        th "А ведь она ничуть не хуже первой.{w} Да и тем более она, вон, с гитарой."
        
    "Поскольку я и сам любил музыку, особенно запрещённый в то время тяжёлый рок, я бросился помогать."
    "Электроник последовал за мной."
    
    show el normal pioneer at sdGetUp
    show mi smile pioneer with dspr
    
    mip "Ой, здравствуйте, ребят. А вы тоже в Совёнок?"
    sdme "Да. Мы пришли спасти тебя от твоей тяжёлой ноши!"
    
    show mi laugh pioneer with dspr
    
    "Девочка рассмеялась."
    
    show mi smile pioneer with dspr 
    
    mip "Спасибо вам. А меня, кстати, Мику зовут. Хотя, когда мы с родителями переехали в Россию, меня записали как Машу, но мама всё равно называет меня Мику."
    mi "Ну а вы меня можете звать, как вам больше нравится. Ой, а сумки для вас не слишком тяжёлые?"
    "Я уже пожалел, что пошёл на это."
    
    hide mi
    hide el 
    with dissolve
    
    extend " Эта девочка перегружала мой мозг слишком быстро. Через две минуты я должен был начать свистеть, как чайник."
    "Электроник довольно спокойно с ней общался, я же занимался тем, что освобождал уютное место для инструмента."
    
    play sound sfx_drop_alisa_bag
    
    "Наконец, вещи были погружены."
    "Товарищ куда-то делся, предоставив мне продолжить общение с привлекательной, но ужасно болтливой японкой."
    
    show mi smile pioneer with dissolve
    
    sdme "Маша... Мику, а ты что играешь?"
    
    show mi surprise pioneer with dspr
    
    mi "В каком смысле?"
    sdme "Ну, какой жанр музыки? Попсу там, классику, может, рок?"
    
    show mi smile pioneer with dspr
    
    mi "Ну, вообще, я играть могу всё, что попросят и на чём попросят, а так в основном душевную музыку люблю. Со смыслом."
    sdme "То есть, балуешься тяжёлой музыкой, всё-таки."
    "Я улыбнулся ей."
    
    show mi laugh pioneer with dspr
    
    mi "Да, вообще-то. Просто у вас в стране с этим какая-то странная проблема. Никогда не думала, что возможно запретить слушать музыку. Это же преступление!"
    
    show mi normal pioneer with dspr
    
    sdme "Тогда посмотри на нас с Электроником. Для нас, вот, нет никаких запретов."
    
    show mi smile pioneer with dspr
    
    mi "Тогда, может быть, вы захотите в музыкальный кружок заглянуть? Я вам уж точно найду занятие по душе, обещаю."
    
    menu:
    
        "Согласиться":
            $ uvroute += 1
            $ ptsmi += mulmi * 2
            $ music += 1
            
            sdme "Обязательно обсудим это. Но меня-то уж точно увидишь - я и сам играю."
            
            show mi normal pioneer with dspr
            
            "Мику одобрительно кивнула."
            mi "И давно ты играешь? А на чём? А насколько профессионально? А какую школу закончил? Выступал где-нибудь? Ох, столько вопросов!"
            th "Саша, ну зачем ты это ляпнул? Она же теперь будет тебя до конца жизни преследовать."
            th "Мне очень повезёт, если мой мозг не уйдёт в перезагрузку."
            sdme "Ну, вообще, я самоучка. У нас дома лежит акустическая гитара, но я всё хочу спаять себе комбик и звукосниматели."
            
            show mi surprise pioneer with dspr
            
            mi "А ты и такое умеешь?"
            "В её глазах было настолько много удивления, что я даже засомневался, что смог бы."
            "Хотя, просматривая с Электроником схемы звукоснимателей, мы решили, что там не так всё сложно. По крайней мере, на первый взгляд."
            sdme "Да ладно тебе, там делать нечего. Тем более для пионера-музыканта!"
            
            show mi laugh pioneer with dspr
            
            mi "Ну, раз такое дело... Удачи вам. А я побегу, в лагере ещё поговорим. Заходи обязательно в наш кружок!"
            "Я кивнул ей. {w}И очередной пионер прыгнул в автобус."
            
            hide mi with dissolve
            
            "Я же глянул в сторону друга и только сейчас заметил, что он спит на лавке."
            
        "Сменить тему":
        
            sdme "Да, подумаем. Так ты говорила, что ты из Японии? Как так получилось, что ты у нас?"
            
            show mi grin pioneer with dspr
            
            "Мой план сработал - Мику тут же забыла про наш разговор про музыку."
            "Правда, теперь она не умолкала ни на секунду, но я посчитал, что эта маля кровь спасёт меня от дальнейшего."
            mi "...Ну и так они познакомились. А потом папа влюбился, ездил к ней каждый год и, когда уже мне было семь, решил перевезти нас сюда, на его родину. А ещё..."
            sdme "Погоди, погоди. То есть училась ты уже у нас?"
            
            show mi smile pioneer with dspr
            
            mi "Ой, да, у меня такой хороший класс был. А учительница наша, Мария Сергеевна, это же просто ангел!.."
            "Не знаю, что меня останавливало заткнуть её и насладиться тишиной, но спасение пришло само."
            mi "Ладно, ещё увидимся! До встречи!"
            
            hide mi with dissolve
            
            "И она тут же исчезла."
            th "Не. {w=.5}Дай. {w=.5}Бог."
            th "Что я хотел сделать?"
            "Я ещё догло соображал, что же буду делать."
            "Вдруг мой взгляд выловил спящего на лавочке друга."
            
    stop music fadeout 4
    "Я толкнул Электроника в бок."
    sdme "Хватит тут сидеть." 
    
    if mtmerc != 0:
    
        extend " Пошли уже в автобус, там дождёмся твоего Шурика."
        
    else:
    
        extend " Пошли уже в автобус."
        
    show el normal pioneer with dissolve
    
    el "Ну, пошли..."
    "Потягиваясь, протянул друг."
    
    scene int_bus_people_day with dissolve
    show el smile pioneer with dissolve
    
    el "Ну что, загрузила тебя Мику?"
    sdme "Ничего. {w=.5}Не. {w=.5}Говори. {w}Мой мозг плавится!"
    
    show el laugh pioneer with dissolve
    
    el "Это ещё ладно. Мой вообще отключился!"
    "Только сейчас я понял, что хочу последовать примеру Электроника - свернуться калачиком и уснуть."
    "Что я и сделал."
    
    hide el with dissolve
    stop ambience fadeout 2
    show blinking
    play soundloop sfx_bus_interior_moving
    
    if ptssl >= 2:
    
        sl "Вставай, пионер! Родина-мать зовёт!"
        
        play music music_list["tried_to_bring_it_back"] fadein 3
        show sl smile pioneer at sdSitCenter with dissolve
        
        "Я посмотрел на нарушителя моего послеобеденного сна и вдруг засмеялся."
        sdme "Мам, ну давай не сегодня!"
        
        show sl laugh pioneer with dspr
        
        "Рядом со мной сидела Славя. {w}Автобус, судя по звукам, уже ехал, а судя по происходящему за окнами, уже давно."
        "Поля сменялись пролесками, за ними мелькали реки..."
        extend " А рядом со мной сидела приятная девушка и смеялась над моими шутками."
        "От такого у меня сразу поднялось настроение."
        
        show sl smile pioneer with dspr
        
        sdme "Долго я проспал?"
        sl "Часа два, не больше. Мы едем-то минут сорок."
        sdme "И остальное время мы просто стояли?"
        
        show sl laugh pioneer with dspr
        
        sl "Кто стоял, а кто разлёгся тут!"
        sdme "Имею право - я грузил вещи."
        "С улыбкой сказал я."
        
        show sl smile pioneer with dspr
        
        sl "Никто особо и не против."
        
        show sl normal pioneer with dspr
        stop music fadeout 4
        
        #У Слави serious - довольно злобная эмоция. Для не очень злобной серьёзности лучше юзать normal.
        
        "Она вдруг стала серьёзной."
        sl "Саш, слушай... Ты точно решил вожатой помогать? Это не так просто, как кажется на первый взгляд."
        sl "Я к тому, что если ты это сказал, чтобы не портить первое впечатление, то ещё не поздно отказаться."
        sdme "То есть ты считаешь меня пустозвоном?"
        th "Какие я, оказывается, слова знаю..."
        
        show sl smile pioneer with dspr
        
        sl "Да нет же! Я хочу помочь тебе избежать ошибки. А то весь твой отдых пойдёт насмарку."
        
        menu:
        
            "\[Помощник\]В приятной компании можно поработать":
            
                if isAssigned == False:
                    $ isAssigned = True
                $ mtmerc = 5
                $ mtmerc_worker = True
                $ mulsl += 0.5
                $ ptssl += mulsl * 1
                
                if not persistent.sdAch_mtmerc:
                    $ persistent.sdAch_mtmerc = False
                    $ sdGetAch("sdAch_mtmerc")

                show sl shy pioneer with dspr
                
                "Девочка опять покраснела."
                th "Что с ней не так? Как ни сделаешь комплимент, так теряется."
                sl "Ты прямо весь день меня смущаешь."
                sdme "Ну а что, это же правда. Да и тем более, мы же пионеры! Мы должны помогать людям."
                
                show sl normal pioneer with dspr
                
                sl "Это да, но, ведь остальные не будут так загружены, как мы."
                sdme "А мы - элита!"
                "Усмехнулся я."
                
                show sl smile pioneer with dspr
                
                sl "Всегда думала, что элита, наоборот, целыми днями отдыхает."
                sdme "Так и мы можем отдыхать. Никто же не заставляет работать."
                
                show sl laugh pioneer with dspr
                
                sl "Нет, вы посмотрите на него. Он ещё даже не поработал, а уже отлынивает!"
                sdme "Ничего подобного! Я готов. Всегда готов!"
                
                show sl smile pioneer at sdGetUp with dspr
                
                sl "Ну раз готов, тогда у нас будут дела в лагере. Вечером увидимся!"
                sdme "Значит, до вечера?"
                sl "Ага. Удачно заселиться!"
                
                hide sl with dissolve
                
                "И она исчезла."
                
            "Пожалуй, ты права, я не потяну":
            
                $ sdChangeMood(-5)
                show sl normal pioneer with dspr
                
                sl "Ты прямо погрустнел."
                sdme "Просто не люблю нарушать обещания."
                
                show sl smile pioneer with dspr
                
                "Она попыталась приободрить меня своей улыбкой."
                "И у неё это, в общем, получилось."
                sl "Не переживай, я поговорю с Ольгой Дмитриевной. Она поймёт."
                sdme "Ладно... В конце концов, в лагере наверняка будет чем заняться."
                sl "Вот именно! Так что, давай, не унывай, а лучше придумай, чем займёшься по прибытию."
                sl "А мы с тобой ещё увидимся. До встречи!"
                
                hide sl with dissolve
                
                "И меня оставили наедине с неприятным осадком."
    else:
        
        "Проснулся я от резкого толчка в бок." with hpunch
        
        show el grin pioneer at sdSitCenter with dissolve
        
        el "Вставай, соня! Разговор к тебе есть."
        "Я лениво потянулся."
        sdme "Ну, давай."
        
        show el normal pioneer with dspr
        
        el "В общем, Шурик хочет уточнить по поводу кружка кибернетики. Ты как, с нами?"
        
        menu:
        
            "Было бы интересно":
            
                $ sdChangeMood(5)
                $ cyber += 1
                
                "Недолго думая, ответил я."
                
                show el smile pioneer with dspr
                
                el "Отлично! Возможно, именно мы станем двигателем прогресса!"
                "Я усмехнулся. Такое я редко слышал от Электроника, но он всегда оказывался прав."
                
            "Я слишком ленивый":
            
                sdme "Ну, ты и сам знаешь."
                
                show el sad pioneer with dspr
                
                el "Ты хоть заглядывать к нам будешь?"
                sdme "Ты так говоришь, будто прощаешься."
                
                show el grin pioneer with dspr
                
                el "Кибернетика - опасная штука. Затянет - не вылезешь!"
                sdme "Это мы ещё увидим."
                "Улыбнулся я."
                
        sdme "И давно мы едем?"
        
        show el normal pioneer with dspr
        
        "Электроник почесал затылок."
        el "Ну, где-то от силы час."
        sdme "Ладно, я дальше спать. Разбуди, как приедем."
        
        show el laugh pioneer with dspr
        
        el "Размечтался! Давай, не спи. Пионер должен быть бдительным!" with hpunch
        sdme "Ладно, ладно, хватит меня трясти." 
        
        hide el with dissolve 
        
    "Я окинул взглядом автобус. Вокруг сидели пионеры, весёлые, неугомонные, неумолкающие ни на секунду."
    
    if ptssl <= 2 and slmet == True:
    
        "Я нашёл взглядом Славю. Она сидела рядом с вожатой и что-то с ней обсуждала."
        th "Наверное, обсуждает мой отказ от помощи."
        
        $ sdChangeMood(-5)
        
        "Снова стало немного некомфортно."
    "Впереди сидела опасная для моего серого вещества Мику и загружала какую-то девочку."
    th "Не завидую я собеседнице этого генератора потока слов..."
    
    if mood >= 95:
    
        $ unspotted = True
        
        "Но что-то выбивалось из этой общей картины."
        "Я долго пытался сообразить, что же, как вдруг мой взгляд упал на соседний ряд."
        
        stop music
        show un normal pioneer at sdSitCenter with dissolve
        play music music_list["waltz_of_doubts"]
        
        "Там в гордом одиночестве сидела девочка."
        
        if slspotted == True:
        
            extend " Как и ожидалось, она была привлекательной."
            
        "По её виду можно было сказать, что с ней случилось что-то плохое."
        "Через какое-то время я будто впитал её настроение."
        "В голове быстро появилась идея подойти к ней и поговорить, но я боялся, что только всё испорчу."
        
        menu:
        
            "Поговорить":
            
                $ uvroute += 1
                $ ptsun += mulun * 2
                $ unmet = True
                
                "Я всё же решил набраться смелости и подойти."
                sdme "Привет. Не против, если я присяду?"
                
                show un shocked pioneer with dspr
                
                "Я тут же пожалел об этом."
                "В её глазах читался чуть ли не животный страх."
                
                show un normal pioneer with dspr
                
                unp "Нет... Садись, конечно."
                
                stop music fadeout 4
                
                sdme "Я - Саша. Из группы Ольги Дмитриевны."
                unp "Я тоже. Меня Лена зовут."
                sdme "Значит, увидимся ещё не раз."
                "Улыбнулся я."
                
                show un smile pioneer with dspr
                
                un "Да, хорошо."
                
                show un normal pioneer with dspr
                
                "Она снова загрустила."
                sdme "А ты чего грустишь?"
                
                show un sad pioneer with dspr
                
                un "Я? {w}Я не... {w}Не знаю. Всегда такое настроение, когда уезжаю куда-нибудь."
                sdme "Да брось! Ты едешь отдыхать в лагерь на целых две недели. {w}Да и компания там неплохая. Ну, скорее всего."
                
                play music music_list["lightness_radio_bus"] fadein 3
                
                "В автобусе заиграло радио, и мне вдруг стало легче общаться с этой девочкой."
                sdme "Тем более, слышишь, вон, даже музыку тебе включили."
                
                show un smile pioneer with dspr
                
                "Лена приободрилась. {w}Видимо, в этой мелодии действительно была какая-то лёгкость и незатейливость, которая сгоняла грусть."
                un "Спасибо тебе. Мне, похоже, надо было просто с кем-то поговорить."
                sdme "Всегда пожалуйста."
                "Улыбнулся я."
                
                stop music fadeout 4
                
                sdme "Если хочешь, я могу ещё с тобой посидеть."
                
                show un surprise pioneer with dspr
                
                th "Видимо, зря я это. Теперь она испугалась."
                "Не знаю, что заставило меня это сказать. Видимо, моё настроение не позволило бы мне, чтобы кто-то грустил рядом."
                
                show un normal pioneer with dspr
                
                un "Ну, если это тебе не помешает..."
                th "Странно. Я-то думал, что она меня пошлёт."
                sdme "Не помешает. Просто, может, тебе некомфортно говорить с человеком, с которым только познакомилась."
                un "Нет, правда, всё в порядке."
                "Мы ещё какое-то время молчали."
                "Я всё недоумевал, почему она так странно себя ведёт, но в итоге нарушил тишину."
                sdme "Лен, а ты одна в лагерь поехала?"
                un "В каком смысле?"
                sdme "Ну, может, со знакомыми. Я, вон, с сестрой и другом поехал."
                
                show un sad pioneer with dspr
                
                un "У меня не так много хороших знакомых..."
                sdme "Извини."
                th "Теперь понятно, почему она такая грустная. Ей попросту некому высказаться."
                th "Наверное, если бы я держал свои проблемы в себе, я бы вообще повесился."
                un "Да нет, ничего страшного. Просто со мной может быть трудно общаться."
                sdme "Ну, вот мы с тобой сейчас общаемся. Это, по-твоему, трудно?"
                
                show un smile2 pioneer with dspr
                
                un "Нет..."
                sdme "Вот видишь! Значит, дело в них, а не в тебе."
                
                show un normal pioneer with dspr
                
                un "Может быть."
                "Тут я вспомнил про Женю."
                sdme "Ладно, Лен. Пойду я искать сестру, увидимся в лагере. Не грусти!"
                
                show un smile pioneer with dspr
                
                un "До встречи!"
                
                hide un with dissolve
                
                th "Какая же странная девочка. Хотя, несмотря ни на что, я ей поднял настроение."
                
            "Оставить всё, как есть":
            
                $ sdChangeMood(-5)
                
                "Лучше не рисковать. Мало ли что у неё случилось..."
                
                stop music fadeout 4
                hide un with dissolve
                
                "Хотя теперь настроение немного упало."
                "Мне всегда было неприятно наблюдать, как кто-то грустит."
                "Вдруг я вспомнил одну вещь."
                
    th "Ладно. Теперь надо найти Женю."
    
    show mt normal panama pioneer at sdSitCenter with dissolve 
    play music music_list["everyday_theme"]
    
    "Я подошёл к вожатой."
    sdme "Ольга Дмитриевна, вы не видели Женю?"
    mt "Видела. Она в передней части автобуса."
    mt "Кстати, она сказала, что не просила никуда её записывать."
    sdme "Она просто не помнит, поэтому и приходится напоминать постоянно."
    "Сказал я, пытаясь отшутиться."
    
    hide mt with dissolve
    
    th "Мой план, похоже, провалился."
    
    show mz normal glasses pioneer at sdSitCenter with dissolve
    
    sdme "Так вот ты где! Чего не разбудила меня?"
    
    show mz laugh glasses pioneer with dspr
    
    mz "Ты слишком сладко спал."
    sdme "Вы всё купили, что хотели?"
    
    show mz normal glasses pioneer with dspr
    
    mz "Да, вроде."
    sdme "Опять три мешка с одними конфетами?"
    
    show mz smile glasses pioneer with dspr
    
    mz "Нет. Всего лишь два."
    sdme "Ну ладно, в библиотеке они тебе пригодятся."
    
    show mz angry glasses pioneer with dspr
    
    mz "Я тебе это припомню, братик!"
    sdme "Не сомневаюсь."
    "Усмехнулся я."
    sdme "Зато поработаешь на благо партии."
    
    stop music fadeout 4
    show mz normal glasses pioneer with dspr
    
    mz "Ладно, потом разберёмся. Нас заселили в один дом."
    
    show mz laugh glasses pioneer with dspr
    
    mz "Так что ты так быстро от меня не отделаешься!"
    sdme "Да, решил тебя взять в лагерь, чтобы отделаться."
    "Съязвил я."
    
    hide mz with dissolve
    
jump sdArrival

label sdArrival:
    show blinking
    $ sd_chapter(1, u"Прибытие.")
    
    "Через некоторое время мы подъехали к Совёнку." 
    "Из окна я увидел две статуи пионеров по две стороны от массивных ворот."
    "Сзади послышался голос Ольги Дмитриевны."
    mt "Ребята, не разбегаемся! Выходим к воротам и там ждём, проведём перекличку и будем расселяться."
    
    scene bg ext_camp_entrance_day with dissolve
    show sh normal pioneer at fright with easeinright
    show el serious pioneer at right with easeinright
    show mz normal glasses pioneer at cright with easeinright
    stop soundloop
    play ambience ambience_camp_entrance_day_people
    
    "Мы с Женей вышли и подошли к Электронику и Шурику."
    
    show mi normal pioneer with easeinleft
    
    "Вскоре к нам присоединилась Мику."
    mi "Ой, а вы тоже у Ольги Дмитриевны?"
    "Я кивнул."
    
    show mi happy pioneer with dspr
    
    mi "Это хорошо, хоть кто-то из знакомых будет."
    th "Очень хорошо..."
    "Тут я осёкся. Да, она довольно разговорчива. Но что в этом плохого?"
    "Подумав немного, я принял вывод, что Мику всё-таки больше приятна, чем раздражает."
    "Её манера общения напоминала приятное щебетание, нежели звуки отбойного молотка."
    
    if ptssl >= 2:
    
        show sl normal pioneer at fleft with easeinleft 
        
        "Вскоре нас заметила и Славя."
        
    if unmet == True:
    
        show un normal pioneer at left with easeinleft
        
        "Лена тоже пришла к нам."
        
    "Ещё несколько людей встало рядом с нами. {w}Видимо, вожатая отправила их ко мне."
    "Наконец, подошла и сама Ольга Дмитриевна."
    
    scene cg d7_pioneers_leaving with dissolve
    play music music_list["what_do_you_think_of_me"] fadein 3
    
    mt "Итак, начнём! Александр!"
    all "Я!"
    "В один голос крикнули мы с Шуриком."
    mt "Так..."
    "Вожатая недоумевающе смотрела на Шурика."
    sh "Меня нет в списке?"
    mt "У меня, видимо, нет."
    sh "Я, скорее всего, записался Шуриком. Мне так привычней."
    "Вожатая сделала паузу."
    mt "Ну ладно, идём дальше. Алиса!"
    dv "Я!"
    mt "Женя!"
    mz "Я!"
    mt "Лена!"
    un "Я!"
    
    if (unmet == False) and (unspotted == True):
    
        th "Так вот как её зовут..."
        
    mt "Мику!"
    mi "Я!"
    mt "Мику, почему ты в скобках написала «Маша»?"
    mi "Ой, простите. Я, наверное, вас запутала? Это моё второе имя, меня в России так записали. А вообще называйте, как вам удобнее будет."
    mt "Ладно, будешь Мику."
    mt "А то мало ли, присоединят к нам какую ещё Машу, переучивайся потом..."
    "Пробубнила вожатая себе под нос."
    "Видимо, её немного раздражала жара, потому что она безуспешно пыталась обмахиваться картонным планшетом, который держала в руке."
    mt "Сергей!"
    el "Я!"
    mt "Что значит «Электроник», Серёж?"
    "Электроник потупил взгляд."
    el "Ну... Это прозвище такое. Друзья всегда так называют."
    "Вожатая вздохнула, на её лице я прочитал: «Да, эта смена будет весёлой...»"
    "В отряде кто-то усмехнулся."
    mt "Ладно. Славяна!"
    sl "Тут!"
    mt "Ульяна!"
    us "Здесь!"
    mt "Сейчас я вам раздам ключи."
    "Ольга Дмитриевна достала огромную свяку ключей, распутала их и протянула нам."
    mt "Саша и Женя, вот." 
    mt "Алиса и Ульяна." 
    mt "Серёжа и Шурик." 
    mt "Лена и Мику." 
    mt "Славя, ты со мной." 
    mt "Ах, да, ребят, если не сможете меня найти - обращайтесь к Славе.{w} Она - моя помощница."
    mt "Номер домика на ключе. {w}Теперь разгружаемся и занимаемся своими делами, после сигнала собираемся у столовой." 
    mt "Если будут вопросы, я на площади." 
    mt "Джентльмены, помогите с сумками."
    
    scene bg ext_camp_entrance_day with dissolve
    show mz normal glasses pioneer with dissolve
    stop music fadeout 4
    
    "Я повернулся к сестре."
    sdme "Жень, я тебе наши сумки отдам, иди заселяться."
    "Сестра кивнула."
    mz "Хорошо."
    window hide
    
    $ sdTimeskip()
    scene bg ext_sky with sdTransition
    stop ambience
    play ambience ambience_camp_entrance_day
    
    window show
    "Наконец, все сумки были выгружены, а мы лежали на газоне и смотрели в небо."
    sdme "Да, отдохнули, называется..."
    "Протянул я."
    sh "Да не переживай, это только первый день."
    sdme "Вот именно! Это меня и пугает!" 
    sdme "Кстати, Эл, мы пойдем к Мику в кружок?"
    el "Сань, когда угодно, но не сегодня.{w} Я и так устал, а ты предлагаешь ещё раз встретить этот генератор случайных выражений."
    sdme "Ладно, пошли, посмотрим, куда мы приехали."
    
    scene bg ext_bus with dissolve
    show sh normal pioneer at right
    show el normal pioneer at left 
    with dissolve
    
    sdme "Ничью сумку не забыли случайно? Сейчас, видимо, автобус за следующей группой поедет."
    
    show el grin pioneer with dspr
    
    el "Да какая разница? Привезут обратно, найдётся пропажа."
    "Я ухмыльнулся."
    sdme "Ну ладно, пошли тогда."
    
    scene bg ext_camp_entrance_day with dissolve
    show sh normal pioneer at right
    show el normal pioneer at left 
    with dissolve
    
    "Мы зашли за ворота и, созерцая все прелести лагеря, побрели в неопределённом направлении под названием «прямо»."
    "Хотя, по большей части, созерцал в основном я. Шурик был слишком увлечён наукой и, в частности, кибернетикой, а Электроник тут уже был."
    
    stop ambience
    play ambience ambience_camp_center_day
    scene bg ext_square_day with dissolve
    show sh normal pioneer at right
    show el normal pioneer at left 
    with dissolve

    "Через некоторое время мы вышли на площадь, где в самом центре стоял одинокий памятник какому-то деятелю."
    sdme "А что за товарищ на постаменте стоит?"
    
    show sh surprise pioneer
    show el surprise pioneer 
    with dspr
    
    sh "Ты что, Генду не знаешь?"
    sdme "Нет."
    "Без тени стыда ответил я."
    
    show el serious pioneer with dspr
    
    el "Так это же покровитель этого лагеря! Он в своё время выделил деньги на его постройку."
    sdme "Если каждому, кто выделяет деньги на постройку чего-нибудь, ставить памятники, то скоро места не останется!"
    
    show el grin pioneer
    show sh normal pioneer 
    with dspr
    
    el "А ты в энциклопедии прочитай про него. Он не только деньги выделял, но и неплохо партии помогал."
    sdme "Если будет время, загляну к сестре."
    sdme "Я её в библиотеку пристроил."
    
    show el laugh pioneer with dspr
    
    el "Ну и правильно! Пусть поработает."
    
    show el normal pioneer at left with dspr
    
    sdme "Ладно, пойду, осмотрюсь тут."
    sh "А мы в кружках будем. Посмотрим, в первую очередь, что за оборудование тут есть."
    sdme "Хорошо. На обед не опаздывайте!"
    el "Не переживай, Сань. Место, если что, тебе займём."
    "Я благодарно кивнул."
    
    hide el
    hide sh 
    with dissolve
    
    "Я подошёл к карте, висевшей на стенде неподалёку и начал её тщательно изучать."
    "За площадью были домики, в нескольких шагах отсюда - столовая, медпункт..."

label sdMapPrepare_1day:    
    $ sdDisableAllZones()
    $ sdDisableAllChibi()
    $ sdSetZone("sd_dining_hall","sdUs_1day")
    $ sdSetZone("sd_clubs","sdClubs_1day")
    $ sdSetZone("sd_boat_station","sdUn_1day")
    $ sdSetZone("sd_music_club","sdMi_1day")
    
    $ persistent.sprite_time = "day"
    $ day_time()
    
label sdMapLoop_1day:
    $ persistent.sprite_time = "day"
    $ day_time()
    stop music fadeout 3
    stop ambience
    stop sound
    
    window hide
    
    $ sdMap_1day += 1
    if sdMap_1day > 1:
    
        $ sdResetZone("sd_boat_station")
        
    if sdMap_1day > 2:
    
        jump sdEvening_1day
        
    else:
        play music dandelion
        $ sdShowMap()

label sdEvening_1day:

    show blink with dissolve
    $ renpy.pause(1)
    $ persistent.sprite_time = "sunset"
    $ sd_chapter(1, u"Вечер.")
    $ sunset_time()
    scene bg ext_square_sunset
    play ambience ambience_camp_center_day
    hide blink
    show unblink
    
    
    if dv_deed_1day == 1:
        jump sdDinnerPrepare
    
    "На одной из лавочек сидела Женя, судя по всему, ожидая меня."
    
    show mz normal glasses pioneer with dissolve
    
    mz "Саша, ты где ходишь? Я уже тебя заждалась!"
    sdme "Решил осмотреться немного, думал, быстро вернусь."
    mz "Ладно, за работу. Нам ещё кучу всего нужно разобрать."
    
    stop ambience
    
    jump sdHouse_1day
    
label sdHouse_1day:

    $ persistent.sprite_time = "sunset"
    $ sd_chapter(1, u"Вечер.")
    $ sunset_time()
    
    scene bg int_house_of_sl_day
    show mz normal glasses pioneer at sdSitRight
    with dissolve
    
    "Через несколько минут я уже раскладывал своё барахло по полкам." 
    "Женя, уже успевшая разместиться, сидела на своей кровати и следила за моим занятием."
    sdme "Ты тут осмотрелась?"
    mz "Да, заглянула в библиотеку."
    
    show mz smile glasses pioneer with dspr
    
    mz "Спасибо, кстати! Довольно спокойное место."
    sdme "Ну вот, а ты не хотела. И никто особо не будет тревожить."
    
    show mz bukal glasses pioneer with dspr

    mz "Не знаю, Саш. Там вообще склад какой-то."
    sdme "В библиотеке? Склад?"
    "Сестра кивнула."
    mz "Уж не знаю, что они там хранят, но Ольга Дмитриевна сказала, чтобы без надобности не открывали."
    mz "Хотя, как бы там ни было, это не особо мешает."
    
    "Я огляделся и заметил на стенах какие-то плакаты."
    sdme "А это что такое?"
    mz "Не знаю. Может, осталось от прошлой смены."
    "Я попытался поддеть и отклеить плакат. Он был намертво приклеен к обоям."
    
    show mz laugh glasses pioneer with dspr
    
    "Сестра растянулась в ехидной улыбке."
    sdme "Чего улыбаешься? Помогла бы лучше."
    mz "Ты думаешь, я не пробовала? Некомфортно, знаешь ли, спать под пристальным взглядом раввина."
    sdme "Можем поменяться, если хочешь."
    
    show mz smile glasses pioneer with dspr
    
    mz "Нет, ничего страшного. Он же с плаката не сойдёт."
    
    show mz normal glasses pioneer with dspr
    
    mz "Саш, ты скоро освободишься? Мне там надо немного разобрать."
    sdme "Не знаю."
    if unneedaid:
        sdme "Хотя, думаю, успеем до того, как Лена проснётся."
    "Я вернулся к распаковке рюкзака."
    sdme "С кем-нибудь познакомилась?"
    mz "Да, Славю встретила на площади. Она пробежала мимо меня раз сто, и всё с какими-то инструментами. Правда, потом успокоилась и разговорилась со мной."
    sdme "И что сказала?"
    mz "Ничего особенного. Природу любит, приехала из Ленинграда, есть младшие братья."
    sdme "И всё?"
    mz "Всё остальное время она рассказывала про лагерь и Ольгу Дмитриевну. Они и в походы ходили, и кино смотрели, и соревнования проводили."
    sdme "Весело."
    
    show mz smile glasses pioneer with dspr
    
    mz "Да. Хорошо, что выбрались сюда."
    "Я закончил раскладывать вещи, и мы отправились к библиотеке."
    
    scene bg ext_library_sunset with dissolve
    play ambience ambience_camp_center_day
    play music music_list["trapped_in_dreams"] fadein 3
    
    "Одноэтажное здание, находящееся в глубине лагеря, встречало нас лозунгами и плакатами."
    
    stop ambience
    scene bg int_library_day
    show mz normal glasses pioneer
    with dissolve
    
    "Войдя внутрь, я почувствовал запах типографии, такой приятный и знакомый. Но в библиотеке царил хаос. Книги лежали на полу в стопках и отдельно, журналы также валялись где попало, полки же, предназначенные для них пустовали."
    sdme "Что здесь случилось? Мамай прошёлся?"
    mz "Видимо. Поэтому я и позвала тебя. Одна не справлюсь."
    sdme "Ладно, давай приступим. Я начну с научной литературы."
    mz "Тогда я займусь художественной."
    
    hide mz with dissolve
    
    "И мы принялись за дело. Сначала мы вытерли все полки от пыли, потом каждый занялся своей секцией."
    "Я перебирал каждую книгу, вытирал её и ставил в алфавитном порядке."
    "Эта скука продолжалась вечность, пока я не заметил на одной из книг автора."
    "Я тут же вспомнил кое-что." 
    sdme "Жень, можно тебя на минутку?"
    
    show mz normal glasses pioneer with dissolve
    
    sdme "Ты ещё не сортировала энциклопедии?"
    mz "Нет, а что такое?"
    sdme"Хочу найти что-нибудь про одного человека."
    "Я показал ей книгу."
    
    show mz smile glasses pioneer with dspr
    
    mz "И давно ты генетиками увлекаешься?"
    "Я повернул обложку к себе. На книге значилось: «Генетические эксперименты и их практическое применение, Генда В. И.»."
    sdme "Не очень. Просто интересно узнать, писал ли он ещё что-то."
    mz "Найдём."
    sdme "А ты можешь книгу на меня записать?"
    
    show mz bukal glasses pioneer with dspr
    
    "Женя удивлённо посмотрела на меня."    
    mz "Запишу. Тебе и правда так интересно?"
    "Я кивнул."
    
    stop music fadeout 3    
    show mz normal glasses pioneer with dspr
    
    mz "Ладно. Тогда я возьму тоже."
    "В её руках появилась книга «Преступление и Наказание»."
    "Женя направилась к ведомости и записала туда всё, что мы взяли."
    
    hide mz with dissolve 
    play music scarytale fadein 2
    
    "Я продолжил наводить порядок, внимательно проверяя авторов, чтобы не пропустить ещё какую-нибудь книгу Генды, но так ничего и не нашёл."
    "В энциклопедиях - тоже ничего." 
    "Я начал сортировать журналы, газеты..." 
    "Бестолку. {w}Генды будто и не существовало в этом мире."    
    "Единственное, что говорило о его существовании - статуя на площади и эта книга."

    stop music fadeout 3
    
    "Женя вывела меня из транса."
    
    show mz normal glasses pioneer with dissolve
    
    mz "Пошли отсюда, здесь мы закончили."
    
    play ambience ambience_camp_center_day
    scene bg ext_houses_sunset 
    show mz normal glasses pioneer
    with dissolve
    
    if dining_worker:
    
        sdme "Слушай, Жень, мне надо помочь в столовой. Я побегу."
        mz "Хорошо."
    
        jump sdDiningHelp
    
    play sound sdNewDinner
    
    if dv_deed_1day == -1:
        jump sdDiningCyber
    
    "Подходя к домику, мы услышали горн, призывающий пионеров к ужину."
    
    if unneedaid == True:
    
        sdme "Жень, я побегу к Лене. Сегодня без меня."
        mz "Конечно, иди."
        jump sdUn_Supper_1day
        
    "В животе заурчало, и я тут же вспомнил, что не ел с самого утра."
    
    if dv_deed_1day == 0:
    
        show mz normal glasses pioneer at left with ease
        show sl normal pioneer at right with dissolve
        
        play music music_list["my_daily_life"]
    
        "По пути к столовой мы встретили Славю."
        sl "А, привет, ребят. Как вам лагерь?"
        sdme "Пока что неплохо. Да и мы тут всего пару часов, непривычно ещё."
        
        show sl smile pioneer with dspr
        
        sl "Ничего, скоро привыкнете."
        sdme "А ты чем занималась?"
        sl "Помогала Ольге Дмитриевне."
        sl "Надо было цветы полить, за складом посмотреть, тут подмести, там подкрасить, в общем, привести лагерь в порядок."
        sdme "Загоняли тебя, наверное?"
        
        show sl laugh pioneer with dspr
        
        sl "Не-е-ет, ты что! Мне, наоборот, нравится."
        
        show sl normal pioneer at right with dissolve
        
        sl "И вообще, у пионера порядок должен быть везде. И в первую очередь - здесь." 
        "Она постучала по виску."
        
        stop music fadeout 3
        
        "Пока мы шли и обсуждали обязанности пионера, я вспомнил, что Сергей был в Совёнке в прошлом году."
        sdme "Слушай, Славь, ты же была тут в прошлую смену?"
        sl "Да."
        sdme "Ты не видела никого из нашего отряда?"
        sl "Лену видела, мы с ней почти всё время болтали. Больше, вроде, никого."
        "Я хмыкнул."
        sdme "Странно, вроде Электроник должен был быть здесь." 
        sl "Я не помню его. Не видела."
        "Она замолчала на некоторое время."
        
        stop ambience
        scene bg int_dining_hall_people_sunset
        show mz normal glasses pioneer at left
        show sl normal pioneer at center
        with dissolve
        play sound sfx_open_door_2
        play ambience ambience_dining_hall_full
        
        "Мы зашли в столовую и, забрав свои порции, расположились на самых уютных местах."
        
        jump sdDiningStandard

label sdDiningStandard:

    $ persistent.sprite_time = "sunset"
    $ sd_chapter(1, u"Ужин.")
    $ sunset_time()
        
    show mz normal glasses pioneer at sdSitDown
    show sl happy pioneer behind mz at sdSitDown
    with dspr
    
    sl "А, нет, вспомнила!"
    "Славя улыбнулась." 
    sl "Я поняла, почему забыла. Он был не в нашем отряде, да и почти всё время в кружках был, а выходил весь мрачный и нелюдимый."
    
    show mz bukal glasses pioneer with dspr
    
    "Мы переглянулись. Наш всегда весёлый и шутливый Электроник ходил... Мрачный?"
    mz "Не похоже на него."
    
    show sl sad pioneer with dspr
    
    sl "Может, случилось что-то. Расстроился, наверное."
    "Я толкнул Женю в бок."
    sdme "Ага, из-за того, что ты на него наорала перед отъездом."
    
    show mz laugh glasses pioneer with dspr
    
    "Мы рассмеялись. У Слави же в глазах блеснула догадка."
    
    show sl normal pioneer with dspr
    
    sl "Точно! Вот на это и было похоже. Как будто он с девочкой поругался."
    
    show mz bukal glasses pioneer with dspr
    
    "Мы посмотрели на неё, как на ненормальную."
    mz "Это невозможно."
    sdme "Невероятно."
    mz "Точно не из-за этого."
    
    show sl sad pioneer with dspr
    
    sl "Почему?"
    sdme "Мы с ним с семи лет общаемся и знаем всё про него."
    
    show sl smile pioneer
    show mz normal glasses pioneer 
    with dspr
    
    "Славя лукаво улыбнулась."
    sl "Вы думаете, он расскажет такое? Это же личное. Тем более, если он влюблён в сестру, как я понимаю, его лучшего друга."
    
    show sl normal pioneer with dspr
    
    "Тут и я задумался. "
    sdme "Ну, не знаю. Если это и так..."
    
    show mz bukal glasses pioneer with dspr
    
    "Женя замахала руками."     
    mz "Да не может этого быть! Он же вечно меня достаёт со своими шутками!"
    
    show sl smile pioneer with dspr
    
    "Мы со Славей хитро улыбнулись."
    sdme "В том-то и дело, сестрёнка!"
    sl "Так он и пытается привлечь твоё внимание!"
    
    show mz normal glasses pioneer with dspr
    
    "Женя вдруг притихла." 
    mz "Ладно, хватит."
    
    show el normal pioneer behind sl at cright
    show sh normal pioneer behind el at fright
    with dissolve
    
    "К нам подошли кибернетики. Они выглядели встревоженными."
    el "Вы Алису не видели?"
    
    show sl laugh pioneer with dspr
    
    "Я и Славя тут же залились смехом."
    sdme "Эх, Серый, Серый..."
    
    show el surprise pioneer with dspr
    
    el "Что?"
    "Мы попытались успокоиться и продолжить разговор."
    
    show sl normal pioneer with dspr
    
    sdme "Да неважно."
    
    show el normal pioneer with dspr
    
    sl "Не видели. А что случилось?"
    
    show sh scared pioneer with dspr
    stop ambience
    play music music_list["sunny_day"]
    
    "Шурик схватился за голову." 
    sh "У нас беда! Огромная беда в лице Алисы!"
    
    show sl surprise pioneer 
    show mz bukal glasses pioneer
    
    sl "Да что такое?"
    
    show sh shocked pioneer with dspr
    
    sh "Есть предположение, что она приготовила дымовые шашки. Из селитры, которая пропала."
    
    show sl scared pioneer with dspr
    
    "Славя позеленела, потом посинела, и впала в отчаяние."
    sl "Так вот почему там был такой бардак!"
    
    show sh upset pioneer with dspr
    
    sh "Это ещё ничего. Она же наверняка не знает химию! Вдруг она сделает слезоточивый газ?"
    "Настало время паниковать всем."
    sdme "Пора уходить отсюда."
    
    hide sl
    hide sh
    hide mz
    hide el
    with dissolve
    
jump sdEscapeDining_1day
        
label sdEscapeDining_1day:

    $ persistent.sprite_time = "sunset"
    $ sd_chapter(1, u"Вечер.")
    $ sunset_time()
    
    play sound sdChain

    "Мы ринулись к двери, я схватился за ручку двери и начал её дёргать. Она подалась, но, звякнув, вернулась в исходное положение." 
    
    stop music
    play music music_list["revenga"]
    
    sdme "Она повесила цепь на ручки!"
    "В одно из окон что-то влетело, и столовая начала заполняться дымом. {w}Поднялась паника, в суматохе я потерял всех."        
    "Вдруг заработал громоговоритель."
    spk "Все на кухню!"
    "Не понимая, что я и где я, я предался течению и вскоре оказался рядом с запасным выходом."
    
    scene bg ext_path_sunset with dissolve
    
    "Там уже разгоралась какая-то сценка." 
    
    stop music fadeout 3
    
    "Я заметил Славю и Женю рядом с Ольгой Дмитриевной и пробился к ним." 
    
    show dv scared pioneer2 with dissolve
    
    if dvmet:
        "Перед нами стояла Алиса."
    else:
        "Перед нами стояла рыжеволосая пионерка из нашего отряда."
    "Ужас исказил её лицо, глаза бегали по пионерам..."
    
    if dv_deed_1day == -1:
        extend " И остановились на мне, одарив порцией гнева."
        
    $ dvmet = True           
    show dv scared pioneer2 at fleft with ease   
    show mt angry pioneer at right with dissolve
    play music music_list["always_ready"]
    
    
    mt "Двачевская, это твоих рук дело?"
    "Ответа не последовало. Его и не требовалось - перепуганная пионерка явно не была похожа на проходящую мимо."
    "Вожатая свирепела всё сильнее."
    
    show mt rage pioneer with dspr
    
    mt "Ты вообще понимаешь, что натворила?! А если кто-то пострадал?"
    mt "Такое поведение неподобающе для пионера!"
    
    show mt surprise pioneer at center with ease
    show sl sad pioneer at fright with dissolve
    
    "Славя обратилась к Ольге Дмитриевне."
    sl "Это и моя вина. Я не заметила, как она вытащила селитру."
    
    show mt normal pioneer with dspr
    
    mt "Славя, не защищай её."
    sdme "Если человек хочет что-то сделать, он сделает это в любом случае."
    mt "Вот именно."
    
    hide sl with easeoutright
    show dv angry pioneer2 at left
    show mt normal pioneer at right 
    with ease
    
    "Пионерка бросила на меня презрительный взгляд, будто говорящий: «тебе это с рук не сойдёт». Я не придал этому значения."
    
    show dv guilty pioneer2 
    show mt angry pioneer 
    with dspr
    
    mt "Так, Алиса, пока мы не решим, что с тобой делать, ты под домашним арестом."
    
    show mt normal pioneer with dspr
    
    sdme "И кто за ней присматривать будет?"
    mt "Да хоть соседка её."
    
    show dv smile pioneer2 with dspr
    
    "Мне показалось, что Алиса ухмыльнулась."
    
    show mt angry pioneer with dspr
    
    mt "Тебе понятно?"
    
    show dv sad pioneer2 with dspr
    
    dv "Понятно."
    
    show mt normal pioneer with dspr
    
    mt "Хорошо."
    
    show mt rage pioneer with dspr
    
    mt "Марш в домик! И чтобы я тебя на улице не видела!"
    "Алиса спокойно зашагала в заданном направлении."
    
    hide dv with dissolve
    show mt angry pioneer at center with ease
    
    mt "И поправь рубашку!"
    stop music fadeout 3
    
    show mt normal pioneer with dspr
    show sl normal pioneer at right with dissolve
    
    "Наклонившись к рядом стоящим Славе и вожатой, я прошептал:"
    sdme "Вам не кажется, что она слишком спокойно отреагировала?"
    mt "А что она тут сможет сделать? Спорить бесполезно, да и она сама знает, что виновата."
    sdme "Всё равно мне это кажется странным."
    mt "В любом случае, инцидент исчерпан. Мы с вожатыми решим, что с ней делать, а пока расходимся."
    
    hide mt with dissolve
    show sl normal pioneer at center with ease
    
    "Мы со Славей развернулись и начали разгонять толпу."
    sdme "Всё, товарищи, не на что тут больше смотреть! Расходимся!"
    
jump sdAfterDinner_1day 

label sdAfterDinner_1day:

    show blink
    $ renpy.pause(1)
    $ sd_chapter(1, u"Вечер. После ужина.")
    scene ext_dining_hall_near_sunset
    show sl normal pioneer
    hide blink
    show unblink
    
    "После того, как все разошлись, мы поднялись на крыльцо столовой."
    
    if us_football_1day and dv_deed_1day == -1:
    
        "Я вспомнил, что Алиса просила меня достать кастрюлю."
        th "Ульянка... Она целый день ошивается в столовой..."
        th "И живут они вместе..."
    
        if ptsus < 2 or mtmerc_worker:
        
            sdme "Славя, мне кажется, мы сделали большую глупость."
            sl "Почему?"
            sdme "Алисе нужна была кастрюля. И, кажется, я знаю, кто её достал."
            
            show sl surprise pioneer with dspr
            
            sdme "Ульянка."
            sl "Откуда ты знаешь?"
            sdme "Я был в столовой сегодня. Нас с ней заставили чистить картошку."
            sdme "И сейчас я понял, почему она была там."
            "Славя задумалась."
            
            show sl normal pioneer with dspr
            
            sl "Знаешь, может, это и так. Но я их совсем не знаю."
            sdme "Думаешь, они не сделали это вместе?"
            sl "Думаю, мы не должны спешить с выводами."
            sdme "Может быть, ты права."
            
        else:
        
            th "Хотя не стоит делать поспешных выводов. Может быть, мелкая тут и ни при чём."
      
    if dv_deed_1day == -1:
    
        "Славя вдруг положила руку мне на плечо."
    
        sl "Саш, я понимаю, как тебе паршиво. Но давай посмотрим на это с другой стороны."
        sl "Ничего же плохого не случилось?"
        sdme "Пожалуй, нет."
        
        show sl smile pioneer with dspr
        
        sl "Вот. Не стоит себя истязать. Пошли, уже темнеет."
    
    else:
    
        sdme "Странный сегодня день... Только приехали и вдруг такое."
        
        show sl sad pioneer with dspr
        
        sl "И не говори. Ещё и я не уследила..."
        sdme "Эй, но ты и не обязана была!"
        sl "Может и так... Но всё равно паршиво."
        sdme "Не переживай. Всё-таки ничего плохого не случилось."
        sdme "Ты не обязана следить за каждым болваном, Славь. Тем более, ты одна, а их сколько?"
        
        show sl smile pioneer with dspr
        
        sl "Ты, наверное, прав. Спасибо, что поддерживаешь меня."
        
        show sl normal pioneer with dspr
        if dv_deed_1day == 1:
        
            $ sdChangeMood(-5)
            th "Паршиво. {w}Как же паршиво. {w}Она же не знает, что я тоже причастен к этому."
            th "Может, рассказать ей всё, как есть?"            
            if ptssl >= 3:
                th "Всё равно моё впечатление будет испорчено."
                th "Хотя, с другой стороны, я буду честен с ней..." 
                th "Она, похоже, доверяет мне. {w}И получит такой нож в спину..."
                
            th "Нет, не стоит. Может, пронесёт, и она не узнает."
        
        sl "Пошли, уже темнеет."
    
jump sdNight_1day

label sdNight_1day:
    $ sdTimeskip()
    $ persistent.sprite_time = "night"
    $ sd_chapter(1, u"Ночь.")
    $ night_time()
    scene bg ext_square_night with sdTransition
    "Уже смеркалось..."
    
    jump sdWIPRoute
    
return

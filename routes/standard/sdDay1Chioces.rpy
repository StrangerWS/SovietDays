#============================== День ================================
#=========================== Муз. кружок ============================

label sdMi_1day:
    $ persistent.sprite_time = "day"
    $ day_time()
    $ uvroute += 1
    $ sd_chapter(1, u"Муз. кружок.")
    scene bg ext_square_day with dissolve
    stop music fadeout 3
    play ambience ambience_camp_center_day
    
    "Я всё-таки решил наведаться к Мику."
    "Нетрудно было догадаться, что она будет в музыкальном кружке."
    
    stop ambience
    scene bg ext_musclub_day with dissolve
    play music music_list["memories_piano_outdoors"]
    
    "Издалека я услышал музыку."
    "Видимо, Мику уже давно всё обустроила и сейчас занималась своим любимым делом."
    "Классическая мелодия завораживала, мне не хотелось останавливать это действо."
    
    scene bg ext_musclub_verandah_day with dissolve
    
    "Я сел на веранду рядом с дверью и слушал."
    
    window hide
    $ renpy.pause(7, hard = True)
    
    "Наконец, я решил прервать представление и вежливо постучать."
    
    play sound sfx_knock_door7_polite
    
    sdme "Можно войти?"
    
    stop music fadeout 1
    
    mi "Конечно! Минутку, сейчас я открою."
    
    play sound sfx_open_door_1
    scene bg int_musclub_day with dissolve
    show mi happy pioneer with dissolve
    play music music_list["so_good_to_be_careless"]
    
    mi "Ой, привет, Саш, хорошо, что пришёл. А я тут как раз аппаратуру проверяю. Оборудование, конечно, слабоватое, но, в общем, играть можно. Ну что, ты обдумал моё предложение?"
    th "Да, что-то я и забыл про её манеры."
    sdme "Я проведать тебя зашёл. Может, помочь чем."
    
    show mi normal pioneer with dspr
    
    mi "А-а-а... Ну, в любом случае, проходи."
    th "Похоже, она немного расстроилась."
    "Я заметил матрасы, лежащие в кружке."
    th "Интересно, зачем? {w}Она тут ночевать собирается?"
    sdme "Тебе уже и матрасы принесли?"
    
    show mi smile pioneer with dspr
    
    mi "Да нет, они тут лежали как только я зашла. Они мне сильно не мешают, да и напрягать никого не хочется. А таскать их замучаешься. Да и полежать в конце концов можно, если устанешь."
    sdme "Ну да, и слушателям будет куда сесть."
    
    show mi laugh pioneer with dspr
    
    mi "Ну да, если они будут. А то я не очень хорошо играю, только распугивать местных обитателей."
    "Рассмеялась Мику."
    sdme "Это мне говорит человек, который секунду назад играл чарующие мелодии?"
    
    show mi shy pioneer with dspr
    
    "Девочка смутилась."
    mi "Да брось, это так, наработки, даже до ума не доведены..."
    th "В таком состоянии она не настолько болтлива... Может, взять это себе на вооружение?"
    sdme "Так надо стараться!"
    
    show mi smile pioneer with dspr
    
    mi "Ну... Я не знаю, скоро закончу её или нет."
    mi "Слушай, Саш..."
    
    stop music fadeout 3
    
    if ptsmi == 2:
    
        extend " Ты говорил, что занимался музыкой."
        
    else:
    
        extend " А ты не хочешь попробовать сыграть что-нибудь?"
        sdme "Знаешь, я так... Умею что-то, но не профессионально."
        mi "Ничего страшного! Вся жизнь впереди, научишься!"
        
    mi "Давай-давай, покажи, на что ты способен."
    "Она протянула мне гитару."
    sdme "Ты мне подыграешь?"
    "Я улыбнулся."
    
    show mi grin pioneer with dspr
    
    mi "Без проблем!"
    
    hide mi with dissolve
    
    "Мику достала вторую гитару и приготовилась играть."
    "Я взял в руки свою и попытался вспомнить хоть что-нибудь."
    "На ум довольно быстро пришла одна мелодия."
    
    window hide
    play music so_long_goodbye fadein 3
    $ renpy.pause(9, hard = True)
    
    "Музыка плавно лилась у меня из-под пальцев, я вспомнил текст этой песни и запел."
    "Мику сидела с гитарой в руках и перебирала струны, подстраиваясь под мою мелодию."
    
    window hide
    $ renpy.pause(20, hard = True)
    $ music += 1
    show mi surprise pioneer with dissolve
    stop music fadeout 5
    
    "Я отставил гитару и вглянул на Мику."
    "Глаза девочки горели восхищением."
    mi "Саша, это шикарно!"
    
    show mi smile pioneer with dspr
    
    mi "И голос у тебя неплохо поставлен!"
    "Я смущённо улыбнулся."
    sdme "Да брось, ничего такого в этом нет... Я просто иногда играю для себя, даже в ноты не всегда попадаю."
    
    show mi grin pioneer with dspr
    
    mi "Не отмазывайся! Я всё слышала своими ушами, ты же помнишь, что я музыкант?"
    "Я всё так же смущённо кивнул."
    
    show mi smile pioneer with dspr
    
    sdme "Ну, тебе виднее."
    
    show mi grin pioneer with dspr
    
    "Мику растянулась в улыбке."
    
    show mi normal pioneer with dspr
    
    mi "А вообще ты как расположился? Осмотрелся тут?"
    sdme "Совсем немного."
    
    show mi smile pioneer with dspr
    
    mi "Это хорошо. А я, вот, как приехала, сюда тут же бросилась, подключать же всё надо, настраивать, столько работы.. А инструменты простаивать не должны..."
    "И я вновь погрузился в её бесконечные рассказы."
    
    $ sdTimeskip()
    scene bg int_musclub_day with sdTransition
    show mi normal pioneer with dspr
    
    "Спустя какое-то время я решил прервать её нескончаемый поток речи."
    mi "...А папа им и говорит: «Зачем вы купили этот холодильник, если старый ещё работает?» А бабушка..."
    sdme "Мику, слушай, мне, наверное, пора. Сестра наверняка не разберётся со всеми вещами."
    
    play music music_list["miku_song_flute"]
    
    "Девочка подняла глаза на меня." 
    "Впервые я встретился с ней взглядом и замер." 
    
    show mi shy pioneer with dspr
    $ ptsmi += mulmi * 1
    
    "Она тоже будто потеряла дар речи." 
    "У меня возникло желание не прерывать этот контакт, но с другой стороны мне почему-то было некомфортно." 
    "Наконец, я отвёл взгляд."
    
    show mi normal pioneer with dspr
    
    mi "Ладно, но ты заходи, хорошо? Я тут всё равно пока одна в этом кружке, скучно всё-таки."
    "Мне захотелось сказать ей что-нибудь тёплое, но я не знал, что именно."
    sdme "Зайду, непременно."
    
    show mi smile pioneer with dspr
    if music >= 2:
        mi "Ой, кстати, может, ты сейчас и запишешься?"
        
        stop music fadeout 3
        
        menu:
        
            "\[Музыкант\] Согласиться":
            
                if isAssigned == False:
                    $ mulsl += 0.5
                    $ isAssigned = True
                $ music = 5
                $ music_worker = True
                $ mulmi += 0.5
                $ muldv += 0.5
                
                if not persistent.sdAch_music:
                    $ persistent.sdAch_music = False
                    $ sdGetAch("sdAch_music")
                    
                sdme "Спрашиваешь! Конечно, записывай!"
                
                show mi happy pioneer with dspr
                
                mi "Отлично!"
                jump sdMi_1day_next
                
            "Отказаться":
            
                sdme "Знаешь, Мику, я не особо музыкален. Да, могу сыграть пару песен для себя, но в коллективе играть... Не для меня."
                
                show mi normal pioneer with dspr
                
                mi "Ладно, ничего страшного. Если передумаешь, всегда можешь в клубах перезаписаться."

    mi "Саш, а ты сильно занят после ужина? Может, заглянешь сюда, потренируешься?"
    sdme "Я? Ну, э-э-э..."
    th "Что бы придумать..."
    sdme "Мне сестре надо помочь в библиотеке... С вещами разобраться."
    sdme "Я, конечно, постараюсь прийти, но..."
    
    show mi sad pioneer with dspr
    
    mi "Ну да, понимаю."
    "Она сказала это таким грустным тоном, что мне стало её жалко."
    "Кажется, она часто такое слышала. Не каждый может вытерпеть столько слов в минуту, проникающих в мозг."
    "Я решил как-нибудь разрядить атмосферу."
    sdme "Слушай, Мику, а тебе обязательно именно репетировать? Может быть, стоит просто прогуляться, поговорить?"
    
    show mi happy pioneer with dspr
    
    mi "Почему нет? Можно и так, раз ты так хочешь."
    sdme "Так будет лучше для меня. Ну вот нет у меня музыкальной жилки, Мику."
    sdme "А мне будет стыдно сидеть и показывать свою бездарность."
    
    show mi smile pioneer with dspr
    
    "Мику улыбнулась и, положив руку мне на плечо, сказала:"
    mi "Ничего ты не бездарный!"
    
    show mi grin pioneer with dspr
    
    mi "Но я тебя понимаю. Я, вот, никогда бы не полезла чинить машину. {w}Ой, нет, полезла же один раз!"
    
    scene bg int_musclub_day_blur with fade2
    play music music_list["two_glasses_of_melancholy"] fadein 3
    
    "Мику снова унесли воспоминания о том, как она вместе с отцом чинила машину, как её волосы пропахли машинным маслом и бензином, как потом пришлось их отмывать..."
    "Я сидел и слушал её звонкий, но мягкий голос, не вникая, что именно она говорит. Подумалось, что её голос и правда создан для музыки."
    "Мне вдруг стало грустно, что я не музыкант и не могу поддерживать с ней разговор."
    "Хотя это и не требовалось... Она и сама неплохо справлялась."
    
    scene bg int_musclub_day
    show mi normal pioneer 
    with fade2
    
    "Тут Мику вспомнила о том, что я вообще-то нахожусь рядом с ней и что у меня могут быть дела."
    mi "Ой, Саш, ты, наверное, торопишься?"
    sdme "С чего ты взяла?"
    mi "Ну как же, мы только приехали, ты даже вещи не разобрал, а я уже тебя заболтала. Да и ты вроде собирался идти."
    
    show mi smile pioneer with dspr
    
    mi "Ну, давай, увидимся на ужине!"
    th "Так просто? Я уже почти смирился."
    "Я не мог вспомнить ни одной ситуации, в которой хищник, имея преимущество, отпускает жертву."
    "Хотя Мику сложно было назвать хищником."
    "Тем не менее, я решил воспользоваться такой роскошью."    
    sdme "До встречи тогда."
    
    show mi happy pioneer with dspr
    
    mi "Да, до встречи. Удачи тебе!"
    "Бодро выпалила Мику." 
    
    hide mi with dissolve
    stop music fadeout 3
    
    th "Какая же всё-таки она противоречивая..."
    "С одной стороны, Мику определённо мне симпатизировала. {w}В качестве собеседника или музыкального партнёра."
    "Но с другой... {w}Я не ошибусь, если скажу, что эта девочка за день успела наговорить столько, сколько мне в общей сумме сказали все представительницы прекрасного пола."
    
    scene bg ext_musclub_verandah_day with dissolve
    show dv angry pioneer2 close with dissolve
    play music music_list["always_ready"]
    
    if dvmet == False:
    
        "Я вышел из кружка и столкнулся нос к носу с рыжей девочкой."
        "Её рубашка была завязана по-спортивному, что сразу давало понять её пренебрежение к пионерскому движению." 
        "Мне показалось, что я её где-то видел."
        "Пока я вспоминал, откуда я знаю её, рыжая оттолкнула меня от двери и, грозно сверкнув глазами, спросила:"
        
    else:
    
        "Я вышел из кружка и столкнулся нос к носу с Алисой."
        "Тут же откуда-то возник ступор из-за нашего неудачного разговора в кружках."
        "Пока я тормозил, рыжая оттолкнула меня от двери и, грозно сверкнув глазами, спросила:"
    
    stop music fadeout 3
    
    $ dvname("Куда прёшь? Не видишь, что ли, люди ходят!")
    window hide
    
    menu:
    
        "Ответить":
        
            $ ptsdv += 1
            window show
            
            "Я растерялся от такой «гостеприимности», но тут же собрался и выдал:"
            sdme "А нечего под ноги лезть!"
            
            show dv shocked pioneer2 with dspr
            
            "Пионерка явно не ожидала такой реакции."
            "Не найдя, что ответить, она протиснулась в музкружок, обернулась на меня и захлопнула дверь."
            
        "Промолчать":
        
            window show
            
            "Я растерялся от такой «гостеприимности», не в силах вымолвить ни слова."
            
            $ sdChangeMood(-10)
            
            show dv smile pioneer2
            
            "Девочка ухмыльнулась, ещё раз сверкнула глазами и скрылась в музкружке."
            
    hide dv with dissolve 
    play sound sfx_close_door_1
            
    "Я пожал плечами и пошёл в сторону площади."

    $ sdDisableCurrentZone()
    
jump sdMapLoop_1day

label sdMi_1day_next:
    $ persistent.sprite_time = "day"
    $ day_time()
    $ sd_chapter(1, u"Муз. кружок. Планы.")
    play ambience ambience_music_club_day
    
    show mi normal pioneer with dspr
    
    mi "Тогда нам надо обсудить концерт в честь приезда."
    sdme "Уже? А ты говорила с Ольгой Дмитриевной?"
    
    show mi surprise pioneer with dspr
    
    "Мику подняла на меня удивлённые глаза, подумала немного, и кивнула."
    
    show mi normal pioneer with dspr
    
    mi "Да, наверное, стоит. Хотя, знаешь, мне кажется, она не будет против. Конечно, надо узнать, но..."
    "Она умолкла. Я решил отложить её раздумья."
    sdme "Ладно, с этим потом разберёмся. Давай решим, кого ещё заманить в нашу банду."
    
    show mi grin pioneer with dspr
    
    "Мику хихикнула."
    mi "Скажешь тоже, банду. Мы же обычные пионеры..."
    sdme "А Джон Бон Джови сразу был рок-звездой? А Скорпионы? Мику, главное начать, а там уже втянемся."
    
    show mi normal pioneer with dspr
    
    mi "Думаю, ты прав. Ладно, у тебя есть кто-нибудь на примете?"
    
    play sound sfx_open_door_kick
    show mi surprise pioneer at fright with ease
    show dv normal pioneer2 at fleft with dissolve
    
    play music music_list["always_ready"]
    
    "Вдруг дверь грубо открыли, и на пороге появилась рыжеволосая пионерка."
    
    $ dvmet = True
        
    show mi upset pioneer with dspr
    
    mi "Алиса! Ты умеешь двери нормально открывать?"
    
    show dv grin pioneer2 with dspr
    
    dv "Ой, простите-простите. Я не помешала случайно?" 
    "Алиса сверкнула глазами. Ей явно было плевать на неудобства."

    if dv_deed_1day == -1:
        
        show dv angry pioneer2 with dspr
        
        "А, Саша. Ну, здравствуй, предатель."
        
        show dv angry pioneer2 at cleft behind mi with ease 
        show mi angry pioneer at center with ease
        
        "Алиса бросилась на меня, но Мику встала перед ней."
        mi "Если ты повредишь мне гитариста, я засуну стойку тебе..."
        "Я остановил её."
        sdme "Тихо, тихо. Стойка нам ещё нужна."
        
        show dv angry pioneer2 at left with ease
        
        dv "Замечательно, за него ещё и заступаются! Сопляк!"
        sdme "Погоди секунду. Не ты ли собралась саботировать столовую?"
        
        show dv shocked pioneer2 with dspr
        show mi dontlike pioneer at right with ease
        
        "Алиса резко замахала руками. Мику тут же стала серьёзной."
        mi "Так… Ты опять за старое?"
        dv "Да он врёт, очевидно же! Зачем..."
        
        show mi rage pioneer with dspr
        
        mi "Затем, Алис! Зачем и всегда! Если ты хоть что-то сделаешь, я тебе..."
        
        show dv grin pioneer2 with dspr
        
        dv "Что? Что ты сделаешь?"
        mi "Узнаешь!"
    
    else:
    
        dv "О, замечательно. В наших рядах пополнение."
       
    show mi dontlike pioneer:
        fright
        sdSitDown
    show dv normal pioneer2 at fleft with dspr
       
    "Мику отвернулась от раздражителя. Я сел на матрас рядом с ней."
    sdme "Можно ввести меня в курс дела?"
    
    show mi upset pioneer with dspr
    
    mi "Извини, Саш. Знакомься - моя сводная сестра-террористка Алиса."
    "Я, мягко говоря, удивился." 
    "Мику, такая легкомысленная и неконфликтная, и Алиса, судя по всему, не ангельского характера, никак не могли ужиться вместе."
    "И, судя по прошедшей сцене, это так и было."
    
    show mi dontlike pioneer with dspr
    
    mi "Ты зачем здесь?"
    
    show dv smile pioneer2 at left with ease
    
    dv "Я же музыкант. Моё сердце не может без музклуба."
    "Усмехнулась Алиса."
    
    show mi normal pioneer with dspr
    
    mi "А честно?"
    dv "Честно. Хочу сыграть что-нибудь на концерте."
    sdme "А с чего ты взяла, что он вообще будет?"

    show dv angry pioneer2 with dspr
    
    dv "Она мне сказала, дурень!"
    
    show mi dontlike pioneer with dspr
    
    mi "Хватит! Либо сиди и не остри, либо убирайся!"
    
    show dv surprise pioneer2 with dspr
    
    "Мику была на взводе. "
    
    show dv guilty pioneer2 at right behind mi with ease 
    
    extend "Алиса тут же утихомирилась, подошла к сестре и обняла."
    dv "Тихо, тихо, успокойся. {w}Извини, мне просто все нервы истрепал этот Шурик."
    sdme "А что с ним?" 
    
    show dv angry pioneer2 with dspr
    
    dv "А ты иди отсюда!"
    mi "Никуда он не пойдёт. Он - наш будущий гитарист."
    
    show dv grin pioneer2 at left with ease
    
    dv "А играть-то он умеет?"

    show mi smile pioneer with dspr
    
    mi "Саш, покажешь?"
    sdme "Ей? Хм-м-м… Нет, пожалуй."
    
    show mi sad pioneer with dspr
    
    stop music fadeout 3
    
    "Мику подвинулась ко мне и сделала такое жалобное выражение лица, что я сломался. {w}Взяв гитару, я начал наигрывать одну из своих любимых песен."
    
    play music wanted_dead_or_alive_cover fadein 2
    
    $ renpy.pause(3, hard = True)
    show mi happy pioneer with dspr
    $ renpy.pause(5, hard = True)
    show dv surprise pioneer2 with dspr
    
    "Глаза Алисы округлились, она одобрительно закивала головой."
    window hide
    
    $ renpy.pause(20, hard = True)
    stop music fadeout 3
    show dv smile pioneer2
    show mi normal pioneer 
    with dspr
    
    dv "Хорошо играешь. Где занимался?"
    sdme "Самоучка."
    dv "Ладно, Мику, ты нашла нам второго гитариста. Теперь за дело!"
    mi "А кто сказал, что ты в группе?"
    
    show dv surprise pioneer2 with dspr
    
    "Алиса остолбенела." 
    dv "Что? Почему нет?"
    
    show mi grin pioneer at sdGetUp with dspr
    
    mi "Да расслабься, шучу я."
    
    show dv normal pioneer2 with dspr
    show mi normal pioneer at right with ease
    play music music_list["everyday_theme"]
    
    dv "Пора распределить роли! Александр, каковы ваши предпочтения?"
    "Я пожал плечами."
    sdme "Мне без разницы, отдаю свою судьбу в ваши руки."
    
    show dv grin pioneer2 with dspr
    
    dv "Тогда ты - соло!"
    "Мику кивнула."
    
    show mi smile pioneer with dspr
    
    mi "Да, Саш, у тебя хорошо получается."
    
    show dv normal pioneer2 with dspr
    
    dv "Мику, как всегда - вокал. Ну, а я - ритм."
    
    show mi happy pioneer with dspr
    
    mi "Если все согласны, значит осталось только найти ритм-секцию."
    
    show dv grin pioneer2 with dspr
    
    dv "О-о-о, я знаю одну любительницу постучать!"
    sdme "Тогда басист за мной."
    "Все были согласны с таким раскладом."
    sdme "Теперь осталось решить, что играть будем."
    
    show mi normal pioneer 
    show dv normal pioneer2 
    with dspr
    
    "Мику достала листок и ручку и быстро начала записывать названия песен. Через пару минут листок был исписан вдоль и поперёк."
    mi "О, а давайте Сашину сыграем?"
    dv "Неплохо было бы."
    sdme "Мику, мне кажется, ты не даёшь нам права выбора. {w}Давайте до вечера выберем по три песни с человека, а потом из них выделим четыре или..."
    
    show mi grin pioneer
    show dv grin pioneer2
    with dspr
    
    midv "Пять! И не меньше!"
    "Хором оборвали меня сёстры."
    sdme "Ладно..."
    
    show mi normal pioneer 
    show dv normal pioneer2 
    with dspr
    
    "В общем, это и меня устраивало. Не зря же я столько времени с гитарой не расставался!"
    "Действительно, когда мне купили гитару, я только и делал, что сидел и разбирал гитарные партии."
    "Сначала это были классические мелодии, которые я находил в самоучителях, но дальше..."
    "В пятом или шестом классе я открыл для себя рок-музыку. Она действительно завораживала меня."
    "Все эти риффы и мелодии, все песни, тексты которых я знал наизусть... Всё это, как говорится, «задевало струны моей души»."
    "Мику вывела меня из раздумий."
    
    show mi smile pioneer with dspr
    
    mi "Я думаю, завтра решим. Да, и не забудьте у своих кандидатов в группу спросить пару песен. {w}А сейчас нам нечего делать, и, тем более, скоро ужин."
    "Алиса кивнула, попрощалась и ушла."
    
    hide dv with dissolve
    stop music fadeout 3
    play sound sfx_close_door_1
    show mi normal pioneer at center with ease
    
    "Я посмотрел на Мику."
    mi "Ты не подумай, она не всегда такая. Дома мы неплохо ладим, иногда играем вместе. {w}Она довольно способная."
    sdme "А сейчас что на неё нашло?"
    mi "Не знаю, честно говоря. {w}Я помню, такое было, когда она общалась с одним мальчиком..."

    show mi grin pioneer with dspr
    $ renpy.pause(1)
    show mi happy pioneer with dspr
    play music music_list["memories"] fadein 2
    
    "Мику хихикнула и продолжила с мечтательным выражением лица."
    mi "Ой, там была любо-о-овь... {w}Он за ней чуть ли не везде, а она... {w}Ну, знаешь, характер. Строит из себя неприступную башню, а на деле... {w}Всё равно потом размягчилась. Сообразила, что к чему, а он уже с другой. {w}Меня к нему ревновала до жути."
    sdme "Ты хочешь сказать..."
    
    show mi grin pioneer with dspr
    
    mi "Не-е-ет, конечно нет!.. {w}Не знаю. {w}Может быть."
    "Она подмигнула мне."
    "Этого жеста я не понял. Она имела ввиду, что я понравился {i}Алисе{\i}?"
    "В любом случае, спрашивать было бы глупо и неприлично." 

    show mi smile pioneer with dspr
    
    mi "Саш, а ты сможешь вечером ещё порепетировать?"
    sdme "Думаю, смогу."
    th "Всё равно я не нашёл никакого занятия на вечер."
    mi "Тогда увидимся!"
    
    scene bg ext_musclub_verandah_day with dissolve
    play sound sfx_close_door_1
    
    "И дверь за мной захлопнулась."
    "Недолго думая, я зашагал на площадь."

    $ sdDisableCurrentZone()
    
jump sdMapLoop_1day

#============================ Столовая ==============================


label sdUs_1day:
    $ persistent.sprite_time = "day"
    $ day_time()
    $ sd_chapter(1, u"Столовая.")
    scene bg ext_square_day with dissolve
    stop music fadeout 3
    play ambience ambience_camp_center_day
    
    "Я решил прогуляться до столовой."
    "Судя по карте, она находилась прямо за площадью, хотя мне пришлось какое-то расстояние пройти по аллее."
    
    scene bg ext_dining_hall_near_day with dissolve
    
    "Наконец, здание оказалось передо мной. Одноэтажное строение с небольшой верандой выглядело приветливо, и я всё-таки решился зайти."
    
    stop ambience
    play sound sfx_open_door_1
    scene bg int_dining_hall_day with dissolve
    play ambience ambience_dining_hall_empty
    
    "В столовой стоял звон, шум и запах еды. Видимо, повара вовсю готовили ужин для только что прибывших пионеров." 
    "Пахло, как ни странно, довольно аппетитно, и я невольно сглотнул слюну."
    nap "Кто там пришёл?"
    "Донёсся мягкий, но звонкий женский голос из кухни. Судя по всему, он принадлежал поварихе."
    sdme "Да я только..."
    nap "Наконец-то! Тебя тут с приезда ждут!"
    sdme "Но я..."
    nap "Проходи, мой руки и марш картошку чистить!"
    "Я вздохнул. Опять работать. Хотя... В столовой неплохо - всегда можно перехватить парочку булок или ещё чего."
    
    scene bg int_kitchen_day2 with dissolve
    
    "Я заглянул на кухню. Здесь стояли довольно крупные женщины лет пятидесяти, которые носились между плитами и помешивали то тут, то там странную розоватую жижу."
    $ usname("Это кисель.")
    
    show us normal sport at fleft with dissolve
    
    "Заявила мне стоявшая рядом с дверью невысокая девочка на пару лет младше меня."
    "Видимо, она заметила, каким презрительным взглядом я смотрел в кастрюлю."
    
    if usmet:
    
        "В девочке я сразу же узнал Ульяну. Неудивительно, что она теперь отрабатывает все проказы."
        
    sdme "Ясно..."
    nap "Ульяна, хватить языком чесать, проведи товарища к рукомойнику и за дело!"
    $ usname ("Пошли!")
    
    show us normal sport at center with ease
    
    "Девочка дёрнула меня за руку, и мы оказались у раковины."
        
    if not usmet:
    
        sdme "Так это ты Ульяна?"
        "Она кивнула."
        sdme "А я - Саша."
        
    us "А тебя за что сюда замели?"
    
    if not usmet:
    
        "Я хмыкнул. Выходит, столовая - своего рода исправительная колония для провинившихся пионеров?"
        
    sdme "За любопытство, наверное. Хотел осмотреться тут."
    
    show us grin sport with dspr
    
    us "Ну, вот и осмотрелся. Хоть мне компанию составишь!"
    "Улыбнулась Ульяна."
    
    if not usmet:
    
        "Я присмотрелся к ней. Девочке было на вид лет четырнадцать, ей положено резвиться на улице в лучах летнего солнца, а она сидит тут, отбывая наказание."
        "Вот так и рушится детство. Я вздохнул. Почему-то мне стало её жалко."
        sdme "Слушай, а что ты такого натворила, что тебя заперли в столовой?"
        
        show us upset sport with dspr
        
        "Ульяна состроила грустную рожицу."
        us "Играла..."
        "Я усмехнулся. Это не осталось незамеченным."
        
        show us angry sport with dspr
        
        us "Чего смешного? Сейчас умою тебя, будешь знать!"
        "Я тут же умолк. Перспектива быть облитым не очень радовала."
        sdme "Просто интересно, как так надо играть, чтобы тебя заперли в столовой помогать."
        
        show us normal sport with dspr
        
    else:
        
        sdme "Так и угодила на общественные работы?"
        
        show us sad sport with dspr
        
        "Пионерка опустила голову."
        us "Как видишь..."
        
    nap "В этом вся Ульянка!"
    "Прокричала повариха."
    nap "Она у нас тут частым гостем в прошлую смену была. Да, Ульян?"
    
    show us sad sport with dspr
    
    us "Ну тётя Надя! Что я сделаю, если Ольга Дмитриевна всё время глаз с меня не спускала?"
    na "Да понимаю я, понимаю. Но всё-таки будь спокойней, а то весь лагерь в лазарет сляжет."
    "Я вновь усмехнулся."
    sdme "Да, с кем мне придётся работать!"
    na "Да ты не пугайся, пионер. Пока она тут готовит, вся поглощена процессом. Только конфеты ей не давай!"
    
    hide us with dissolve
    
    "Закончив отмывать свои пыльные лапы, я зашагал на кухню."
    sdme "Так с чем вам надо помочь?"
    na "Сейчас, сейчас..."
    "Тётя Надя исчезла в дальней комнате. Через некоторое время там послышались грохот, звон и неприличные выражения."
    sdme "Вам помочь?"
    na "Не надо."
    "Наконец она вышла из комнаты, волоча за собой мешок."
    na "Забирай. Это картошка. Вам надо почистить хотя бы половину."
    "Мои глаза округлились. Мешок весил килограммов двадцать, не меньше. Даже на двоих это заняло бы порядочное количество времени."
    sdme "Куда же столько?!"
    na "Ты знаешь сколько у нас пионеров?"
    sdme "Ну..."
    na "Вот и не возникай. Не управитесь - мы доделаем."
    "Я вздохнул с облегчением. Это действительно помогло бы. А ещё больше помогло бы, если бы они всё сделали за нас. Ну, или по крайней мере за меня."
    window hide
    
    $ sdTimeskip()
    show cg d1_dining_work_2 with sdTransition
    stop ambience
    play music music_list["smooth_machine"] fadein 3
    
    window show
    "Вскоре мы уже сидели над картошкой и пыхтели. {w}Таз медленно наполнялся очищенными овощами, а я всё больше и больше уставал. "
    "Я следил за Ульянкой: ей определённо нравилось быть на кухне. "
    "Конечно, ей нравился не процесс чистки картошки, а работа поваров - она постоянно отбегала, заглядывала в каждую кастрюлю и спрашивала, как они это делали."
    "Когда она в очередной раз вернулась, я спросил:"
    sdme "В повара метишь?"
    us "Я? Не-е-ет. Мне усидчивости не хватает следить за супом или кашей столько времени."
    sdme "Ну, с возрастом придёт."
    us "А почему интересуешься? Жену себе ищешь?"
    "Она расплылась в хитрой улыбке."
    sdme "А ты в кандидаты записаться хочешь?"
    us "А что, нельзя?"
    "Я усмехнулся."
    sdme "Не рановато ли ты об этом задумалась?"
    us "Лучше рано, чем никогда!"
    sdme "Сдаётся мне, там было другое слово."
    us "Да какая разница... Чисть свою картошку!"
    "Осадила она меня."
    "Мы умолкли на какое-то время."
    "Вдруг я заметил на себе её взгляд."
    sdme "Чего?"
    us "Саш..."
    sdme "Ну?"
    us "А пошли в футбол играть!"
    sdme "Дочистим - и хоть на луну."
    us "Ты что? На луну - долго! А я хочу эту смену на всю катушку отгулять."
    "Я почесал голову. Давно я не бегал по полю с мячом, да и расслабиться после такого дня не помешало."
    sdme "Ну давай, ладно. А мы управимся до ужина?"
    us "Должны. Вот тебе и стимул будет!"
    "Усмехнулась Ульянка. "
    "Да, она была ещё ребёнком. {w}Гиперактивным, весёлым и позитивным ребёнком. "
    "Пожалуй, это мне в ней и нравилось. Да и сам я в её годы был таким же."
    sdme "Хорошо."
    
    $ us_football_1day = True
    
    "С улыбкой сказал я."
    "Вдруг мне в голову пришла идея."
    sdme "Ульян?"
    us "М-м-м?"
    sdme "Давай кто быстрее картошку дочистит!"
    us "Э-э-э, нет! Ты сейчас одну почистишь и скажешь, что всё!"
    "Она совсем по-детски надулась."
    sdme "Да брось! Разделим пополам, чтобы честно было."
    "Я и впрямь честно разделил картошку на две «равные» части, забрав себе все крупные клубни. "
    "Не знаю, для чего я это сделал. То ли вызов хотел бросить, то ли Ульяну переутруждать не хотелось."
    
    scene cg d1_dining_work_1 with fade3
    
    "На счёт «три» мы бросились крошить бедную картошку. В итоге моя горка убывала гораздо быстрее. "
    "Я замедлился, но когда последняя картошина из моей кучи отправилась в таз, Ульянка успела очистить только половину."
    sdme "Готово, шеф!"
    "Выкрикнул я и отгрёб из её кучи нехилую часть."
    us "Эй! Давай по-честному!"
    sdme "То есть?"
    us "Я сама дочищу! Иди, гуляй, занимайся своими делами."
    "Я взглянул на неё строгим взглядом."
    sdme "Ещё чего! Чтобы ты всю славу забрала? Обойдёшься!"
    us "Ах, так!"
    "Она швырнула в меня картошкой, которая тут же улетела куда-то под плиту."
    us "Иди, доставай!"
    sdme "Сама кинула - сама доставай."
    "Ульянка посмотрела на меня упрекающим взглядом."
    us "Ну ладно... Я тебе это припомню!"
    "Она отправилась искать убежавшую в недра кухни картофелину, а я быстро принялся очищать остатки." 
    "Когда откуда-то из-под плиты раздалось триумфальное «ура!», вся картошка была очищена."
    us "Эй! А меня подождать?"
    sdme "Не удержался."
    "Усмехнулся я."
    
    scene bg int_dining_hall_day with dissolve
    stop music fadeout 3
    
    "Я оттащил таз к поварам, а когда вернулся, Ульянка уже куда-то исчезла."
    th "Подожду, пожалуй. Тем более, в футбол же хотели поиграть."
    "Я сел в столовой, внимательно осматривая помещение."
    "Обычная советская столовая: несколько столов, расставленных в четыре ряда, обычные мотивирующие плакаты на стенах и колоннах, обычная плитка..."
    "Я растянулся на двух стульях, прикрыл глаза и, сам того не ожидая, задремал."
    window hide
    
    scene anim sdDream with fade3
    play music music_list["sparkles"] fadein 3
    $ set_mode_nvl()
    
    window show
    "Снилось мне, будто меня заперли в какой-то средневековой штуке. Вокруг люди; я смотрю на них через дырки, которыми испещрена моя камера."
    "Судя по всему, здесь, на площади какой-то праздник - люди веселятся, поют и развлекаются, здесь множество разных палаток, в каждой происходит что-то своё: где-то продают сладости, где-то клоуны показывают шоу, где-то плюются огнём..."
    "Но больше всего народа около меня. Я не знаю, что сделал и почему тут оказался. Видимо, я был каким-то мошенником или протестующим. "
    "Люди смотрят прямо в мои глаза и смеются. Я встретился с каждым взглядом и вдруг среди всей этой живости и веселья я заметил единственный взгляд сочувствия. "
    "Я присмотрелся внимательней в эти глаза; они показались мне знакомыми, но моя память не давала понять кто это."
    "Я явно видел женские черты, но только терялся в догадках."
    "Почему она так много значит для меня здесь?"
    "Может, это моя мама? {w}Сестра? {w}Или кто-нибудь знакомый? {w}Может быть, во сне она приходится мне женой?"
    "Контакт прервался: передо мной встал палач. Слева от него на сцене возник глашатай."
    drgl "Приветствую всех! {w}Сегодня в гостях у герра Шнайдера вновь оказался отступник!" 
    "Толпа заревела."
    drgl "И сегодня вы присутствуете при испытании новой камеры пыток!"
    drgl "Названия ему ещё не дали, но вы только посмотрите на это! Это последнее слово военной техники!"
    drgl "Герр Шнайдер изволит показать вам, как легко входит и выходит копьё в эти дырки!"
    "Палач двинулся, вознёс копьё, продемонстрировав его всем, развернулся ко мне. Замах..."
    window hide
    
    scene bg int_dining_hall_day with flash_red
    play sound sdGrunt
    $ set_mode_adv()
    stop music fadeout 3
    
    sdme "Ай!"
    "Проснулся я от резкого тычка в бок." 
    
    show us grin sport with Dissolve(2)
    
    extend " Рядом стояла Ульянка и довольно ухмылялась."
    
    play music music_list["i_want_to_play"] fadein 3
    
    sdme "Ты чего творишь?"
    "Буркнул я, потирая свой бок."
    us "А нечего дрыхнуть на рабочем месте!"
    "Усмехнулась она и помахала мне рукояткой от вилки."
    sdme "Ах ты!.."
    "Я было кинулся на неё, но она быстро отбежала."
    
    show us dontlike sport with dspr
    
    us "Ты чего, шуток не понимаешь?"
    sdme "Да ну тебя! Я чуть не умер во сне от этого твоего копья!"
    "Я показал на вилку."
    
    show us surp2 sport with dspr
    
    us "Чего-чего?"
    "Я вкратце пересказал ей сон."
    "Подумав немного, я решил спросить."
    sdme "Ульян, а ты веришь в то, что сны - это отражение реальности?"
    
    show us normal sport with dspr
    
    us "В каком смысле?"
    sdme "Ну, например палач этот... Вдруг это какой-нибудь мой недоброжелатель?"
    
    show us laugh sport with dspr
    
    us "А-а-а, так вот что!"
    "Хихикнула Ульяна и лукаво посмотрела на меня."
    sdme "Что?"
    us "Ты думаешь, что эта девушка реально существует, вот что! И ты хочешь узнать кто это."
    "Я неохотно признался.{w} Меня удивило, что девочка её возраста так быстро догадалась об этом."
    
    show us grin sport with dspr
    
    us "Эх, Саша, Саша… Не переживай, у тебя вся жизнь впереди. Найдём мы тебе твою загадочную незнакомку!"
    sdme "Э-э-э, кто мы? Не надо, я и сам справлюсь!"
    us "Ну, наше дело предложить, ваше дело отказаться!"
    
    stop music fadeout 3
    
    "Тут я заметил, что за спиной у Ульянки было что-то спрятано. Я присмотрелся и заметил, что это был..."
    "Мешок с конфетами?!"
    th "Ах ты маленькая воровка!"
    
    menu:
    
        "Ничего не делать":
            
            $ ptsus += 1
            
            th "Да ладно, она же ребёнок. Пусть побалуется."
            
            show us surp2 sport with dspr
            
            "Ульяна, видимо, догадалась, что я раскрыл её преступление."
            "Я ей подмигнул, чтобы показать, что я не сдам её."
            
            show us shy sport with dspr
            
            "Она тут же затараторила."
            us "Ой, мне бежать надо, я забыла..."
            "Взглядом я ей показал, чтобы она думала быстрее."
            
            show us surp1 sport with dspr
            
            us "...э-э-э… У меня вещи не разобраны! Вот! Увидимся после ужина!"
            "И она убежала."
            
            hide us with dissolve
            
            "Я вздохнул, покачал головой и вышел из столовой."
            th "Дети, что с них взять…"
            
            scene ext_dining_hall_near_day with dissolve
            
            "Выйдя, я чуть не наступил на горсть конфет. Судя по всему, их оставила Ульянка. На упаковке значилось: «Красный Октябрь»."
            th "Губа не дура! Самые дорогие конфеты утащила!"
            "Я засунул их в карман и, насвистывая, отправился на площадь."
            
        "Вернуть конфеты":

            show us surp2 sport with dspr
        
            "Ульяна, видимо, поняла, что я увидел, потому что тут же начала отодвигаться назад."
            us "Ой, Саш, слушай, мне бежать надо..."
            sdme "Ага, ага. Пакетик только отдавай и иди."
            
            show us sad sport with dspr
            
            us "Какой пакетик?"
            "Я указал ей за спину."
            
            show us angry sport with dspr
            
            us "Это моё!"
            sdme "Конечно, твоё. Я, вот, у тебя его заберу, он тоже мой будет! А ну иди сюда!"
            
            play music music_list["doomed_to_be_defeated"]
            hide us with easeoutright
            
            "Я рванул к ней, она же уже оказалась за дверью."
            
            scene bg ext_dining_hall_away_day at sdRunning with dissolve
            play soundloop sfx_run_forest
            
            "Наш марафонский забег начался от столовой..."
            
            $ renpy.pause(3, hard = True)
            scene bg ext_musclub_day at sdRunning with dissolve
            
            "...мимо музклуба..."
            
            $ renpy.pause(3, hard = True)
            scene bg ext_shower_day at sdRunning with dissolve
            
            "...душевых..."
            
            $ renpy.pause(3, hard = True)
            scene bg ext_path_day at sdRunning with dissolve
            
            "...уходя в лес..."
            
            $ renpy.pause(3, hard = True)
            scene bg ext_polyana_day at sdRunning with dissolve
            
            "...и закончился капитуляцией Ульянки на лесной опушке." 
            
            stop music fadeout 3
            stop soundloop
            play sound sfx_bodyfall_1
            scene ext_polyana_day
            show us cry sport at sdSitCenterS 
            with dissolve
            
            "Хоть она и была проворней и быстрее меня, зато я оказался внимательней и вовремя заметил корень дерева, который остановил воровку."
            sdme "Набегалась? Теперь пошли возвращать!"
            "Ульяна ничего не ответила, подняв на меня слезившиеся глаза."
            us "Ты... Ты..."
            "Я заметил ссадину на её коленке. Не очень большую, но неприятную."
            sdme "Так, спокойно. Дай мне секунду."
            
            show us angry sport with dspr
            
            us "Не трогай! Ты уже достаточно сделал!"
            sdme "Хочешь, чтобы туда заражение попало? Пошли, промоем рану."
            
            show us sad sport close at sdCarryingS with dspr
            
            "Я подхватил её и понёс к умывальникам."
            
            scene ext_path_day
            show us normal sport close at sdCarryingS 
            with dissolve
            
            sdme "Где же они были..."
            us "Кто?"
            sdme "Умывальники. Где ещё промыть можно?"
            us "Ну, в речке..."
            sdme "Уж лучше в луже! Не надо."
            us "Тогда нам направо."
            "Ульяна ещё некоторое время показывала дорогу, пока мы не вышли к нужному нам месту."
            
            scene ext_washstand_day
            show us shy sport at sdSitCenterS
            with dissolve
            
            "Я усадил её на землю, промыл рану и обвязал галстуком."
            sdme "Болит?"
            us "Не сильно."
            sdme "Стоили эти конфеты того?"
            
            show us surp2 sport with dspr
            
            "Ульянка вдруг округлила глаза."
            us "Конфеты! Куда они подевались?"
            sdme "Сиди, найду я их. И верну!"
            
            show us dontlike sport with dspr
            
            "Она надулась."
            sdme "А потом приду сюда. Далеко не уходи."
            
            scene ext_polyana_day with dissolve
            "Я решил отложить несколько штук, для компенсации вреда."
            "Найдя пакет, я отправился в столовую."
            
            scene int_dining_hall_day with dissolve
            
            $ sdChangeImpression(10)
            
            na "Так вот куда они делись!"
            "Воскликнула тётя Надя, когда я рассказал всю историю."
            sdme "Тёть Надь, можно я для неё парочку конфет возьму? Всё-таки это я виноват, что она упала."
            na "Да, бери, конечно. Только предупреди её, что это последний раз, когда это сходит ей с рук!"
            "Я было собрался уходить, как она окликнула меня."
            na "Саш, может ты поможешь нам на кухне в эту смену? У тебя вроде неплохо получается."
            
            menu:
            
                "Согласиться":
                    
                    if isAssigned == False:
                        $ mulsl += 0.5
                        $ isAssigned = True
                    $ dining = 5
                    $ dining_worker = True
                    $ mulus += 0.5
                    if not persistent.sdAch_dining:
                        $ persistent.sdAch_dining = False
                        $ sdGetAch("sdAch_dining")
                    
                    "Я задумался."
                    th "А что мне, собственно, ещё тут делать? В любом случае, работа непыльная."
                    sdme "А давайте!"
                    na "Отлично! Тогда мы тебя ждём на пять минут раньше!"
                    sdme "Хорошо, я приду."
                    
                "Отказаться":
                    
                    th "Работать в столовой?"
                    "Я поёжился."
                    "Я вспомнил, как в школе дежурил в столовой. Не лучшая перспектива, честно говоря."
                    sdme "Знаете, я, пожалуй, воздержусь."
                    na "Ну ладно. Но если передумаешь - заходи!"
                    "Она улыбнулась."
                    
            scene bg ext_dining_hall_near_day with dissolve
            
            "Я вышел и направился к умывальникам."
            
            scene bg ext_washstand_day 
            show us normal sport at sdSitLeftS
            with dissolve
            
            "Ульянка так и сидела, потирая рану. Я присел рядом."
            sdme "Как нога?"
            us "Вроде лучше, ничего страшного. Что сказали?"
            sdme "Что лучше просто попросить."
            "Я протянул ей горсть конфет."
            
            show us surp3 sport at sdGetUp with dspr
            
            "Глаза у Ульянки загорелись."
            us "Ты... Как? И зачем?"
            sdme "Говорю же, попросил. Тем более, мы с тобой их заработали."
            us "Саша... Ну спасибо тебе!"
            
            show us shy sport with dspr
            
            "Она сосвем незаметно обняла меня и убежала."
            
            hide us with easeoutright
            $ ptsus += 2
            
            th "Да, Саша, ты точно пользуешься популярностью в этом лагере."
            "Я улыбнулся и пошёл на площадь."
            
    $ usmet = True
    $ sdDisableCurrentZone()
    
jump sdMapLoop_1day

#========================== Кибернетика =============================


label sdClubs_1day:
    $ persistent.sprite_time = "day"
    $ day_time()
    $ sd_chapter(1, u"Кружки.")
    scene bg ext_square_day with dissolve
    stop music fadeout 3
    play ambience ambience_camp_center_day
    
    "Я решил всё-таки посетить товарищей."
    
    scene bg ext_clubs_day with dissolve
    
    "Здание клубов находилось около ворот, поэтому я уже был знаком с его внешним видом, и мне не пришлось долго искать."
    "Недолго думая, я зашёл внутрь."
    
    stop ambience
    scene bg int_clubs_male_day with dissolve
    play ambience ambience_clubs_inside_day
    play sound sfx_open_door_clubs
    
    "Поскольку никуда, кроме мастерских, мои знакомые не могли зайти, я направился в эту секцию."
    "Здесь было довольно уютно, хоть и стоял типичный заводской запах."
    "У нас он вечно стоял на уроках труда и дома, когда дед ещё работал на заводе."
    "Потом он плюнул на это дело и устроился в школу сторожем.{w} Или завхозом. {w}По его занятиям это было непонятно."
    "Можно сказать, он работал на двух работах, хотя толком не платили ему даже за одну."
    
    show el normal pioneer at left
    show sh normal pioneer at right
    with dissolve
    
    "В центре всего этого заводского благолепия стояли юные умы Советского Союза и рассматривали какую-то книгу."
    sdme "Ну что, товарищи кибернетики, какие ваши планы?"
    
    show sh surprise pioneer with dspr
    
    "Шурик подскочил, с его носа съехали очки, которые он ловко поймал."
    
    hide el with dissolve
    show sh serious pioneer at center with ease
    
    sh "Ты чего так подкрадываешься? А если бы я паяльник держал?"
    sdme "Извини. Что читаете?"
    
    show sh normal pioneer with dspr
    
    sh "«Основы кибернетики для начинающих»."
    sh "Эти дилетанты считают, что без должного образования невозможно собрать радиопередатчик!"
    sdme "А что, возможно?"
    
    if cyber > 0:
    
        show sh surprise pioneer with dspr
    
        "Шурик недоумевающе посмотрел на меня."
        sh "А ты точно занимался этим?"
        sdme "Я же шучу. Хотя в каждой шутке...{w} Я бы смог спаять пару «передатчик-приёмник», если бы тут были схемы."
    
        show sh normal pioneer with dspr
    
        sh "Это не обсуждается. Схемы долго рисовать. А мы тут всё-таки практикуемся."
        
    else:
        
        show sh smile pioneer with dspr
        
        sh "Как раз плюнуть!"
        sh "Ты сомневаешься в наших способностях?"
        sdme "Нет, просто..."
        
        show sh normal pioneer with dspr
        
        sh "Что?"
        
    sdme "Вы этим хотите заняться?"
    sh "Да, пока что. Но не только. Смотри. Это наша конечная цель."
    "Шурик развернул какую-то бумажку и на ней оказалась схема робота."
    "Довольно крупного робота, надо сказать."
    sh "Вот. По этой схеме у него должны двигаться и руки, и ноги. Но можно его собрать так, что он тебе тацевать будет."
    sdme "Ого!" 
    
    if cyber > 0:
        extend " И, ты думаешь, успеем?"
    
        show sh smile pioneer with dspr
    
        "Товарищ усмехнулся."
        sh "Успеем! Корпус у нас есть, даже пару деталей уже собрали предшественники. Дело за малым."
        "Я кивнул."
        sdme "Тогда... С чего начнём?"
    
        show sh normal pioneer with dspr
    
        sh "Мне нравится твой настрой."
        sh "Сначала попробуем пойти по простому пути. Раз уж ты вызвался собрать передатчик и приёмник... Вот тебе схемы. Если что-то понадобится, детали в той комнате."
        "Он махнул рукой на соседнюю дверь."
        
    sh "А, да, кстати. Серёг, нам надо притащить сюда пару вещей, помнишь?"
    
    show sh normal pioneer at right with ease
    show el normal pioneer at left with dissolve
    
    el "Японское оборудование? Притащим."
    sh "Время уже пришло. Ольга Дмитриевна просила подойти в три часа. Сейчас половина четвёртого."
    el "Пошли тогда."
    sh "Иди с Саньком, я тут пока что посмотрю схемы. Может, определим, чего нам не хватает."
    
    stop ambience
    scene bg ext_clubs_day with dissolve
    show el normal pioneer with dissolve
    play ambience ambience_camp_center_day
    
    "Мы с Элом вышли из мастерских."
    "Друг выглядел погруженным в какой-то очень сложый технический процесс."
    sdme "Непривычно видеть тебя в таком сосредоточенном состоянии."
    el "То есть?"
    sdme "За всё время разговора ты в книжку смотрел."
    el "А, ну да. Зачитался."
    sdme "Бывает. Куда нам?"
    
    show el serious pioneer with dspr
    
    el "Они в прошлую смену в спортзале кассеты смотрели. Новая смена началась, а телевизор никто не убрал."
    sdme "Так нам эту дуру тащить придётся?"
    
    show el upset pioneer with dspr
    
    "Электроник кивнул и опустил голову."
    el "Приехали, называется, отдохнуть."
    "Я усмехнулся."
    sdme "Да, вы с Шуриком прямо на отдых приехали!"
    
    show el grin pioneer with dspr
    
    el "Кибернетика, товарищ, для нас - лучший отдых!"
    sdme "Я вот понять не могу, ты шутишь или серьёзно?"
    
    show el normal pioneer with dspr
    
    el "Не знаю сам. Мне это интересно, но всё-таки неохота проводить всё свободное время за напильником и сварочным аппаратом."
    sdme "Поэтому мы и приехали сюда. Чтобы отвлечься."
    
    show el sad pioneer with dspr
    
    "Электроник понизил голос."
    el "Чтобы отвлечься, нужно от Шурика убегать."
    el "Он просто кибернетический маньяк какой-то!"
    "Я усмехнулся."
    sdme "На это у тебя есть я!"
    
    show el grin pioneer with dspr
    
    el "Тогда я на тебя надеюсь!"
    sdme "Ладно, пошли уже."
    
    stop ambience
    scene bg ext_playground_day with dissolve
    show el normal pioneer with dissolve
    play ambience ambience_soccer_play_background
    
    "Мы вышли на футбольное поле."
    "Вокруг резвилась малышня, бегая туда-сюда с мячом, некоторые играли в баскетбол, пара людей была на теннисом корте..." 
    "Больше я ничего не успел рассмотреть."    
    
    "Потому что точно в меня прилетел футбольный мяч." 
    
    play sound sfx_soccer_ball_kick
    show effect_colorOverlayRed
    play addsound sdGrunt   
    with hpunch
    with vpunch
    $ renpy.pause(0.25)
    hide effect_colorOverlayRed with dspr
    
    window hide     
    show blink
    "По инерции я повалился на землю, но сил встать у меня уже не было."
    "Не знаю, куда делась моя резвость и энергичность, но сейчас я больше всего хотел полежать."
    el "Саня?"
    el "САНЯ?!"
    el "Кто это сделал?"
    $ usname("Ой...")
    $ usname("Простите, пожалуйста, я не хотела...")
    "Я, конечно, не потерял сознания, да и удар был не настолько сильный, но мне было ужасно интересно, чем это закончится."
    "Но когда я услышал женский голос..."
    "Меня сбила с ног... "
     
    play sound sfx_scary_sting
    show effect_thatgirl at sdEffect_thatgirl with flash
    $ renpy.pause(1.3)
    hide sdEffect_thatgirl with Dissolve(2.5)
    
    th "Ладно уж. Полежу. Тем более, мне это полезно."
    "Видимо, за этот день погрузки/разгрузки я знатно устал."
    el "Надо его как-то привести в сознание."
    $ usname("Как?")
    el "Я знаю один способ. Но это придётся делать тебе."
    "Я изо всех сил пытался не заржать."
    th "Серёга, тварь, ты что там задумал?"
    $ usname("Не-е-ет!")
    el "Ты знаешь ещё способы?"
    $ usname("Водой можно... Я сейчас!")
    el "Стой, хулиганка!"
    el "Искупляй вину."
    $ usname("Может, договоримся?")
    el "Пионеры не продаются!"
    "Девочка вздохнула, и я вдруг почувствовал прикосновение в области грудной клетки."
    th "Всё, пора возвращаться к жизни."
    th "Пока это не зашло слишком далеко."
    
    scene bg ext_sky
    show us shy2 sport close
    hide blink
    show unblink
    
    play music music_list["eat_some_trouble"] fadein 2
    
    if usmet == False:
    
        "Рядом сидела девочка лет четырнадцати."
    else:
    
        "Рядом сидела Ульянка."
        
    extend " И тянулась к моим губам!"
    
    sdme "Здравствуй!"
    "Лукаво улыбнулся я."
    
    show us surp3 sport close with dspr
    
    "Девочка вскрикнула от испуга."
    $ usname("Так ты... {w}Притворялся?")
    
    show us angry sport close with dspr
    
    $ usname("Сейчас я тебе устрою!")
    sdme "Для начала слезь с меня."
    
    show us shy sport close with dspr
    
    $ usname("Извини.")
    
    stop music fadeout 3
    scene bg ext_playground_day with dissolve
    show us shy sport with easeinbottom
    show el grin pioneer at cleft behind us with easeinleft 
    
    "Мы поднялись на ноги."
    "Недалеко стоял Электроник и довольно ухмылялся."
    el "Ну что, проснулась лягушка? Не успела в принца превратиться?"    
    
    play music flash_strike fadein 3
    
    sdme "Я тебе сейчас устрою лягушку, растлева... {w}растлельщи... {w}тьфу, блин!"
    
    show el scared pioneer with dspr
    
    sdme "Засранец ты!"
    "Я, конечно, сказал всё это в шутку, но Серёга испугался по-дикому. Поэтому я не стал выходить из образа."
    
    show us grin sport with dspr
    
    $ usname("Стой, сначала я!")
    sdme "Погоди, девочка, мы с тобой потом поговорим."
    
    show us angry sport with dspr
    $ renpy.pause(0.2)
    hide us with easeoutright
    scene bg ext_playground_day at sdRunning
    show el scared pioneer at center
    with dspr
    play soundloop sfx_run_forest
    
    sdme "Беги, пока можешь!"
    
    $ renpy.pause(3, hard = True)
    
    "Мы пробежали несколько десятков метров, после чего я сделал резкий рывок и повалил юного сказочника на траву."
    
    stop soundloop
    
    scene bg ext_playground_day
    show el scared pioneer close
    with dspr
    
    "Я обхватил его голову одной рукой, а вторую сжал в кулак и начал делать круговые движения по макушке."
    sdme "Ты что творишь, а?"
    el "Девушку тебе помогаю найти, идиот!"
    el "А ты сразу с кулаками."
    sdme "Да ты посмотри на неё! Ей даже шестнадцати нет!"
    
    show el grin pioneer close with dspr
    
    el "Да хорошо, хорошо. Только встань с меня! Иначе точно не то подумают."
    
    hide el with dissolve
    
    "Мы поднялись на ноги."
    
    show el normal pioneer at sdSitCenter with easeinbottom
    $ renpy.pause(0.3)
    show el normal pioneer at sdGetUp 
        
    stop music fadeout 3
    
    "Наконец-то друг увидел, что у меня с лица не слезает улыбка."
    el "Чего ты ржёшь?"
    sdme "Как тут не ржать, если мой друг - идиот?"
    el "Спасибо."
    el "Я серьёзно испугался, когда ты вырубился."
    sdme "Да не вырубился я. Встать не мог, лень было, устал."
    
    if usmet == False:   
    
        sdme "А тут ты... С этой..."
        usp "Ульяной!"
        
    else:
    
        sdme "А тут ты... С этой Ульяной!"
    
    play music music_list["i_want_to_play"]
    show el normal pioneer at left with ease
    show us grin sport at right with easeinbottom
    
    "Девчонка вылезла из-за моей спины."
    us "Ну, Саша, раз ты, как я поняла, тоже жертва, предлагаю устроить этому кудрявому «тёмную»!"
    sdme "Тише, тише. Ты чего?"
    
    show us angry sport with dspr
    
    us "Я-то чего?"
    us "Это меня заставили лезть тебя целовать!"
    sdme "А ты-то и не была особо против."
    
    show us grin sport with dspr
    
    "Девочка ухмыльнулась."
    us "Да, знатно я тебе приложила мячом!"
    sdme "Кстати, о птичках."
    
    show us sad sport with dspr
    
    us "Ну прости, я правда не специально."
    sdme "Да ладно тебе. Нормально всё."
    
    show us grin sport with dspr
    
    us "Так что мы будем с этим товарищем делать?"
    sdme "Товарищ, что с тобой делать?"
    
    show el grin pioneer with dspr
    
    el "Понять и простить!"
    sdme "Нельзя, Серый, нельзя. Ульянка крови хочет."
    
    show el scared pioneer with dspr
    
    sdme "Да ты не бойся, она слегка. Палец тебе отгрызёт и дело с концом."
    
    play sound sdChatteringTeeth
    
    "Ульяна, видимо вошла в роль и громко клацнула зубами."
    
    hide el with easeoutleft
    show us grin sport at center with ease
    
    "Друг поспешил скрыться."
    sdme "Ну вот, напугали юного двигателя прогресса. У него, может, душевная травма теперь."
    us "У него? {w}Сомневаюсь."
    sdme "Ладно, побегу догонять."
    
    stop music fadeout 3
    show us normal sport with dspr
    
    "Я уже двинулся вслед за испуганным Электроником, как меня дёрнули за рукав."
    us "Стой!"
    sdme "Стою."
    
    show us grin sport with dspr
    
    if not usmet:

        us "Может, сыграешь с нами потом?"
        "Я почесал голову. В принципе, неплохо было бы размяться."
        sdme "Можно. Правда, не знаю когда, мне сейчас перетащить кое-что нужно."
    
    else:
    
        us "Ты мне обещал в футбол сыграть!"
        sdme "Ульян, прости. Меня отправили помочь кое с чем во благо партии."
    
    show us smile sport with dspr
    
    us "И что же это что-то?"
    sdme "Да так... По мелочи. Телевизор и пару кассет."
    
    show us surp1 sport with dspr
    
    us "Ого! Значит, тут и телек есть!"
    "Зря я это сказал."
    "Ульяна, судя по всему, уже готова была поделиться этой новостью с окружающими, но я её остановил."
    sdme "Слушай, я... {w}В общем, это секрет."
    "Конечно, я не знал, как отнесётся вожатая, если все дети в лагере прилипнут к экрану телевизора, но что-то мне подсказывало, что ничего хорошего из этого не выйдет."
    
    show us upset sport with dspr
    
    us "Ну вот! Только нашла себе развлечение, и тут..."
    "Тут у неё в голове промелькнула какая-то идея. Судя по её взгляду, мне она вряд ли понравилась бы."
    
    show us smile sport with dspr
    
    us "Слушай, Саш..."
    us "Раз это секрет, я его сохраню. {w}Но при одном условии."
    "Я вздохнул. Мне казалось, что ничем хорошим это не кончится."
    sdme "Что же ты хочешь?"
    
    show us grin sport with dspr
    
    us "А ты подумай! {w}Ребёнок только что узнал, что тут есть телевизор."
    sdme "Это ты тут ребёнок?"
    "Усмехнулся я."
    
    show us angry sport with dspr
    
    "Я хотел было напомнить про подкол Электроника, но Ульянка резко поменялась в лице, и я понял, что лучше воздержаться."
    us "А что, не похожа? Тебе доказать?"
    sdme "Нет-нет, верю, ладно."
    "Я боялся подумать, как она собралась это доказывать." 
    th "А если она начнёт кататься по земле в истерике и бить ногами? Или, наоборот, носиться вокруг с криками?"
    th "Я же не умею детей успокаивать!"
    
    show us grin sport with dspr
    
    us "В общем, давай, ты как-нибудь меня туда проведёшь?"
    sdme "Ты что, с дуба рухнула? Как я это сделаю?"
    
    show us surp1 sport with dspr

    us "Ну пожа-а-алуйста!"
    us "Ты же, вон, с этим... Серёжей общаешься. Может, выпросишь ключ от кружков?"
    "Вот тебе здравствуйте."
    sdme "А ты откуда знаешь, что он в кружках?"
    
    show us shy sport with dspr
    
    us "Неважно."
    
    show us smile sport at sdJumping with dspr
    
    us "Ну что?"
    "Девочка начала прыгать на месте, ожидая моего ответа."
    sdme "Ну...{w} Ничего не обещаю, конечно..."
    
    show us surp1 sport at center with dspr
    
    us "Спасибо! В футбол играть приходи!"
    
    hide us with easeoutright
    
    "И она тут же убежала."
    
    $ usmet = True
    $ us_watch_tv_2day = True
    
    "Я же побрёл к спортзалу в раздумьях."
    th "Неужели, подслушивает? Значит, надо быть осмотрительнее и не болтать лишнего."
    th "Да и вообще никогда не стоит болтать лишнего."
    th "Блин! Зачем я сказал ей про этот ящик?"
    
    scene bg int_sporthall with dissolve
    stop ambience
    play music music_list["smooth_machine"]
    
    "Я зашёл в просторный и прохладный спортзал."
    "Здесь были различные снаряды, на полу - разметка для футбола и баскетбола, места для болельщиков, но ни одного пионера."
    th "Видимо, детей сюда загоняют во время дождя..."
    "Звуки эхом отдавались по всему залу."
    el "Саня!"
    
    show el scared pioneer with dissolve
    
    "Ко мне подбежал Электроник."
    el "Ну что, она ушла?"
    sdme "Ушла, ушла. Ты чего так испугался?"
    
    show el serious pioneer with dspr
    
    el "Ты не видел, что она с другими делает!"
    sdme "А вы уже встречались?"
    
    show el normal pioneer with dspr
    
    el "Мимолётом... Она около кружков за каким-то пацаном гонялась."
    el "Он резвый малый был... Пока не встретился с ней."
    sdme "И что с ним стало?"
    el "Ничего, в принципе. Сейчас в медпункте лежит."
    sdme "Ого!"
    
    show el smile pioneer with dspr
    
    el "Да не пугайся. Споткнулся о камень, а она на него сзади налетела. Ногу подвернул из-за неё."
    sdme "Ладно, как бы там ни было, нам надо телек вытащить."
    mt "Наконец-то!"
    
    stop music fadeout 3
    show el normal pioneer at left with ease
    show mt grin pioneer at right with dissolve
    
    mt "А я уж и не надеялась, что вы вспомните."
    sdme "И давно вы нас ждёте?"
    mt "Дай подумать..."
    
    show mt angry pioneer with dspr
    
    extend " Где-то час!"
    mt "Мальчики, вы в курсе, что заставлять ждать даму некультурно?"
    elme "Да, Ольга Дмитриевна."
    mt "Тогда марш за грузом и в кружки его! Пионеры..."
    "Последнюю фразу она прошипела себе под нос."
    
    hide el with dissolve
    show mt normal pioneer at center with ease
    
    mt "Извините, ребят. Первый день, а уже столько нервов..."
    sdme "А в чём проблемы?"
    mt "Ну... Я вижу, вы в курсе насчёт нашей неугомонной Ульяны."
    mt "Я сегодня целый день за ней погромы и травмы разгребаю."
    "Я подхватил телевизор, Электроник взял его со своей стороны, и мы пошли."
    "Ольга Дмитриевна уже держала для нас открытой дверь."
    
    play soundloop sfx_far_steps
    
    sdme "Не переживайте. Она же просто ребёнок. Выплеснет энергию и угомонится."
    mt "Хотелось бы верить, Саш. Хотелось бы верить..."
    "Пробормотала она себе под нос, после чего встряхнула головой и сказала:"
    mt "Удачи вам, ребят. Не разбейте его по дороге!"
    "Мы протиснулись в дверной проём и оставили вожатую наедине со своими мыслями."
    
    scene ext_playground_day with dissolve
    play ambience ambience_soccer_play_background
    
    sdme "Серёг, а ты Ульянке говорил, что в кружках сидишь?"
    el "Нет, я с ней вообще-то и не разговаривал почти. А что?"
    sdme "Просто я ей про телек сболтнул."
    sdme "Она раскапризничалась, что хочет посмотреть. Ну и сказала, что у тебя ключи можно достать."
    el "Может, видела, как я кружки открываю, обычное дело."
    el "Так, погоди... Ты согласился?"
    "Я кивнул."
    el "Так это я растлеватель? Или растлельщик? Или как там?"
    sdme "Эй! Я же не заставляю её целоваться с кем попало!"
    el "Саня, Саня... Ничего ты не понимаешь в советской медицине."
    sdme "Всё я понимаю! Сейчас уроню эту дуру на тебя и будет перелом!"
    sdme "Просто дай мне ключи и всё."
    el "Ну ладно, ладно. Только давай дотащим уже эту хреновину."
    "Дальше мы двинули молча."
    "Но не тут-то было. Малышня заметила ящик и тут же окружила нас."
    sdme "Эл!"
    "Прошептал я, пока дети были ещё далеко."
    el "Чего?"
    sdme "Ни слова, что телек - рабочий!"
    el "Кому?"
    sdme "Им!"
    "Я кивнул на зрителей, которые уже собрались вокруг нас."
    kids "Ого! Японский!"
    kids "А посмотреть можно?"
    kids "А куда вы его?"
    kids "А давайте к нам в домик!"
    sdme "Обойдётесь!"
    el "Дети, телевизор сломан..."
    sdme "...Перегорел. Несём чинить."
    el "Когда починим не знаем."
    sdme "Вопросы есть?"
    "Перебивая друг друга, мы придумали отмазку."
    "Делегация грустно разбрелась по площадке, а мы, переглянувшись, зашагали дальше."
    th "Надо было Ульянке так сказать. Чего я не подумал?"
    th "Эх! Ладно, что сделано, то сделано."
    
    stop ambience
    scene ext_clubs_day with dissolve
    play ambience ambience_camp_center_day
    
    "До кружков мы дошли без происшествий."
    "Мне тут же бросилась в глаза распахнутая настежь дверь."
    sdme "Как думаешь?.."
    el "Не знаю. Может, вожатые пристроили куда, может, за деталями убежал."
    el "Сейчас занесём, и я поищу его."
    "Мы протиснулись в отсек кибернетики."
    
    stop ambience
    scene int_clubs_male_day with dissolve
    play ambience ambience_clubs_inside_day
    
    "Поставив телевизор на один из столов, мы свалились на стулья."
    
    stop soundloop
    show el normal pioneer at sdSitCenter with dissolve
    
    el "Сань, а ты можешь в кладовке разобрать и телевизор туда занести?"
    sdme "Ох... Ну, я попробую, но занести не обещаю."
    el "Ну ладно. Я пойду искать Шурика."
    "Я кивнул."
    
    hide el with dissolve
    
    "Я встал и открыл дверь в кладовку."
    
    play sound sfx_open_door_clubs_nextroom
    stop ambience
    scene int_clubs_male2_night_nolight with dissolve
    
    "Свет прочертил линию в кладовке, где на краю я заметил какое-то движение."
    "Я сделал шаг вперёд, и..."
    
    play sound sfx_close_door_clubs_nextroom
    scene black with fade
    play music music_list["orchid"] fadein 2
    
    "... Дверь захлопнулась."
    sdme "Шурик?"
    sdme "Шурик, это ты? Что за шутки?"
    "Мороз пробежал по коже. Оказавшись в полной темноте, я занервничал."
    "Я попытался найти выключатель."
    window hide
    
    $ renpy.pause(1.5)
    
    window show
    "... Безуспешно."
    th "Приехали. Вот так и помру, даже не узнав, что меня убило, в какой-то кладовке в глуши."
    "Тут я услышал шаги. Двигаться было опасно, поскольку я не знал, что находится в паре шагов от меня."
    
    stop music fadeout 3
    play sound sdSwitchEarNoise
    scene white with flash
    $ renpy.pause(1.5)
    
    "Резкий свет ослепил меня, и на несколько секунд я потерял ориентацию в пространстве."
    "А когда открыл,"
    
    scene int_clubs_male2_night
    show dv rage pioneer2
    with flash
    
    play music music_list["heather"] fadein 3
    if not dvmet:
        extend " увидел перед собой разъярённую пионерку."
    else:
        extend " увидел перед собой разъярённую Алису."
    $ dvname("Что тебе нужно?!")
    "Злобно прошипела она."
    sdme "Это ты у меня спрашиваешь? {w} Что {i}тебе{/i} надо здесь?"
    
    show dv angry pioneer2 with dspr
    
    $ dvname("Не твоё дело!")
    "Я опешил."
    sdme "Нет, это как раз-таки моё дело. Ты пытаешься своровать казённое партийное имущество."
    $ dvname("Ничего подобного!")
    sdme "Ну, раз ничего подобного, то и парочка наших кибернетиков ничего плохого не найдут, если увидят тебя здесь. {w}Тем более, они на подходе."
    
    show dv scared pioneer2 with dspr
    
    "Пионерка знатно напугалась."
    $ dvname("Ты не сделаешь этого.")
    sdme "Почему же?"
    "Я сделал шаг к двери и тут же был резко остановлен."
    
    show dv guilty pioneer2 with dspr

    $ dvname("Слушай... Может, ты просто поможешь мне?")
    sdme "Как я могу помочь человеку, которого даже не знаю?"
    
    show dv smile pioneer2 with dspr
    
    if not dvmet:
    
        dvp "Как это не знаешь? Я, вот, Алиса."
        sdme "И что это меняет?"
        $ dvmet = True
        
        show dv grin pioneer2 with dspr
        
        dv "Как это что? Теперь мы с тобой знакомы, а значит, ты мне поможешь!"
        sdme "Сомневаюсь."
        
    else:
    
        dv "Как это? А как же группа и всё такое?"
        sdme "Если мы играем в группе, это ещё не значит, что мы - лучшие друзья навеки."
    
    show dv angry pioneer2 with dspr
    
    dv "Тогда все узнают, что ты изнасиловал бедную пионерку прямо в первый день!"
    sdme "Ты так не поступишь."
    
    show dv grin pioneer2 with dspr
    
    dv "Что, думаешь, у меня кишка тонка?"
    "Я остановился. Ну что такого опасного может сделать девочка-подросток с вещами из мастерской?"
    "С другой стороны, выставить меня маньяком гораздо легче."
    "Мне надо было принять решение."
  
    menu:
        
        "Помочь Алисе":
        
            $ dv_deed_1day = 1
            $ ptsdv += muldv * 2
            if cyber > 0:
                $ cyber -= 1
            $ sdChangeImpression(-10)
                
        "Сдать кибернетикам":
            
            $ dv_deed_1day = -1
            $ ptsdv -= 1
            $ sdChangeImpression(5)
            $ cyber += 1
            
            th "Надо кончать с этим. Подыграю, затяну время до прихода кибернетиков и там решим, что делать."
    
    sdme "Ну ладно, посвяти меня в курс дела."
    
    stop music fadeout 3
    show dv normal pioneer2 with dspr
    
    dv "Мне нужно немного селитры, бумага, кастрюля и что-нибудь, куда можно было бы запихать смесь."
    sdme "Ты... Что собралась с этим делать?"
    dv "Дымовые шашки!"
    "Моя челюсть отвисла."
    sdme "За... Зачем?"
    
    show dv grin pioneer2 with dspr
    
    dv "Чтобы развлечься, конечно!"
    sdme "Ну у тебя и развлечения!"
    dv "Это ещё что! Я запру их на замок, чтобы никто не выбежал!"
    
    if dv_deed_1day == -1:
    
        "Я нервно сглотнул. Похоже, в нашем лагере завёлся террорист."
    
    else:
        
        th "Хм... Это может быть весело."
        th "Я бы однозначно хотел это увидеть!"
    
    sdme "Ладно. Я попытаюсь помочь, чем смогу."
    
    play soundloop sfx_far_steps
    show dv surprise pioneer2 with dspr
    
    "Вдруг в соседней комнате послышались шаги. Алиса умолкла."
    el "Саня! Ты где?"
    
    stop soundloop
    
    $ sdDisableCurrentZone()
    
    
    if dv_deed_1day == 1:
    
        jump sdDvClubs_1day
        
    elif dv_deed_1day == -1:
    
        jump sdCyberClubs_1day

label sdDvClubs_1day:

    $ persistent.sprite_time = "day"
    $ day_time()
    $ sd_chapter(1, u"Кружки. Алиса.")
    
    sdme "Я выведу их из кружка, а ты незаметно проскочишь."
    "Шепнул я пионерке."
    
    show dv normal pioneer2 with dspr
    
    "Алиса кивнула."
    sdme "Сиди тихо и не делай глупостей. Всё, что надо, я подготовлю."
    el "Саня, тебя там завалило что ли?"
    sdme "Иду уже... Пытаюсь! Тут хрен знает что за бардак!"
    
    play sound sfx_drop_pipe
    
    "Я погремел какой-то балкой для виду, кивнул напарнице и вывалился из двери."
    
    play sound sfx_open_door_clubs_nextroom
    scene bg int_clubs_male_day with dissolve
    play sound sfx_bodyfall_1
    with vpunch
    
    show el normal pioneer with dissolve
    
    "Друг был один."
    sdme "Эл, а где Шурик?"
    el "Поэтому я и здесь. Я его не нашёл, пошли! Мне кажется, он на складе."
    "Я удивился такой удаче."
    
    scene bg ext_clubs_day
    show el scared pioneer with dspr
    with dissolve
    play ambience ambience_camp_center_day
    
    "Мы вышли на улицу, и тут я услышал, как мимо нас кто-то прошмыгнул."
    el "Ты слышал?"
    sdme "Может, ветер?"
    el "Хмм… Не знаю. Как будто… Из кружков?"
    sdme "Да брось, Серый. Я там был. Ты думаешь, я бы не заметил?"
    
    show el serious pioneer with dspr
    
    el "Кто знает… Может, ты скрываешь что-то."
    "Я нервно сглотнул. Догадался?"
    
    show el grin pioneer with dspr
    
    el "Да ладно, я шучу. Пошли."
    "Я незаметно выдохнул. Мы отправились в путь."

    show el normal pioneer
    
    sdme "Слушай, Серёг, можно задать странный вопрос?"
    el "М-м-м?"
    sdme "Не знаешь, где селитру достать можно?"
    el "А тебе зачем? Неужели для удобрения?"
    sdme "Ты ни за что не поверишь. {w}Да."
    
    show el surprise pioneer with dissolve
    
    "Электроник удивлённо поднял бровь."
    sdme "Славя попросила. Она с цветами возится… Может, для них."
    el "Ну, на складе есть, наверное. Только..."
    
    show el serious pioneer with dspr
    
    extend " Ты же знаешь, что это опасно?"
    sdme "Конечно."
    el "Вся ответственность за опасные химические вещества на вожатой. Если что-то случится…"
    sdme "Сначала промоют мозги ей, а она уже нам. Знаю, знаю. Не переживай."
    
    scene bg ext_warehouse_day
    show el normal pioneer at left
    show sh normal pioneer at right
    with dissolve
    
    "Мы пришли к складу. Шурик и правда был там."
    el "Шурик, ты где ходишь? Мы тебя обыскались."
    sh "Да вот, жду, когда откроют склад. Там есть несколько хороших схем и радиодеталей."
    sh "Громкоговорители неработающие и всё такое. Правда, меня просили их спаять... Но это неважно."
    sdme "А почему просто не взять?"
    
    show el surprise pioneer
    show sh surprise pioneer
    with dspr
    
    "На меня посмотрели, как на умалишённого."
    sh "Ты что?! Нам за это такое устроят! Не стоит оно того."
    el "Тебе настолько нужна эта селитра?"
    
    show sh upset pioneer
    show el normal pioneer
    with dspr
    
    "Шурик округлил глаза."
    sh "Селитра? Вы что, дымовые шашки задумали сделать?"
    sdme "Нет, это Славя просила, она..."
    
    show sh serious pioneer with dspr
    
    sh "Славя за ключами пошла."
    "Моя ложь в момент рухнула."
    
    show el serious pioneer with dspr
    
    el "Так что ты задумал?"
    sdme "Ничего." 
    sh "Саша, говори. Ты заодно с этой Алисой?"
    
    show el normal pioneer with dspr
    
    el "С кем?"
    sh "Она зашла в кружки перед моим уходом. Я сказал, что если бы у меня и была селитра, ей бы я её точно не дал."
    sdme "Я даже не знаю, кто это."
    
    show el serious pioneer with dspr
    
    sh "Тогда зачем тебе селитра?"
    sdme "Я объяснял."
    sh "Ладно, Сергей, я за Славей, а ты следи за ним. А то нам точно несдобровать."
    
    hide sh
    
    "Шурик ушёл, а Электроник преградил мне путь в склад."
    
    play music music_list["i_want_to_play"] fadein 2
    
    show us grin sport at fright with easeinright
    
    "Тут внезапно появилась Ульянка, подмигнула мне и со всей скорости врезалась в Электроника."
    
    show us grin sport at left with ease
    show el shocked pioneer with dspr
    $ renpy.pause(0.5)
    hide us with easeoutleft 
    show el angry pioneer with dspr
    
    el "Ах ты мелкая!.."
    
    hide el with easeoutleft
    
    "И он убежал. Я хмыкнул."
    th "Видимо, она заодно с Алисой"
    "Подумал я, дёргая за ручку склада."
    
    stop music fadeout 3
    
    "Заперто."
    "Я обошёл его со всех сторон, как вдруг сзади заметил дыру. Она была за забором и вела в лес."
    "Поднапрягшись, я перемахнул через преграду и влез внутрь."
    
    stop ambience
    
    scene bg int_warehouse_day with dissolve
    
    "Тут было темно, но я прекрасно видел все надписи на ёмкостях и в итоге нашёл калиевую селитру."
    "Она стояла на самом верху."
    th "И лестницы даже нет…" 
    "Я попробовал дотянуться до неё. Ёмкость пошатнулась, накренилась и упала прямо мне в руки." 
    
    play sound sdFallingCans
    
    "За ней полетели пустая банка с краской, коробка с гвоздями, ведро..."
    "Я тут же вылетел оттуда и побежал в лес."
    
    scene bg ext_path_day with dissolve
    
    "Сделав крюк, я пошёл к нашему с Женей домику."
    
    scene bg int_house_of_sl_day with dissolve
    
    "Я запихнул селитру в шкаф и завернул в свои вещи."
    th "Теперь куда?"
    "Поразмыслив немного, я решил вернуться на площадь."
    
jump sdMapLoop_1day


label sdCyberClubs_1day:

    $ persistent.sprite_time = "day"
    $ day_time()
    $ sd_chapter(1, u"Кружки. Расправа.")
    
    "Я бегом ринулся из кладовки."
    
    play sound sfx_open_door_clubs_nextroom
    scene int_clubs_male_day 
    show sh surprise pioneer at right
    show el surprise pioneer at left
    with dissolve
    play sound sfx_bodyfall_1
    with vpunch
    
    "Кибернетики смотрели на меня, как на умалишённого."
    sdme "Там... Там..."
    sh "Что?"
    sdme "У нас гость."
    
    show sh serious pioneer 
    show el normal pioneer
    with dspr
    
    sh "Я догадываюсь. Рыжая девчонка?"
    "Я кивнул."
    sh "Алиса. За селитрой пришла."
    "Я ещё раз кивнул."
    
    show sh upset pioneer with dspr
    
    sh "Плохи наши дела. Селитру используют в кустарном производстве дымовых шашек. Но это ещё не всё."
    sh "Если она додумается - а она додумается - она добавит туда и другие реагенты."
    sdme "И что тогда?"
    sh "Ну, побочные эффекты могут быть в виде осадков сажи, изменения цвета... "
    
    show sh scared pioneer
    
    extend "Или ядовитого газа."
    sdme "Тогда нам надо не дать ей это сде..."
    
    hide el
    hide sh
    window hide
    
    play music music_list["awakening_power"]
    play sound sfx_body_bump
    with hpunch
    with vpunch
    
    "Закончить я не успел, потому что сзади получил резкий толчок в спину и прилетел животом в стул."
    "Скорчившись от боли, я повалился на пол, видя, как две пары ног пытаются преградить путь ещё одной паре."
    "В итоге им не удалось это сделать, и меня вновь вернули в вертикальное положение."
    
    show el surprise pioneer at left
    show sh serious pioneer at right
    
    el "Ты как?"
    sdme "В порядке, вроде бы. Нам надо остановить её!"
    
    show el normal pioneer with dspr
    
    sh "Успокойся, ничего она не сделает. Она не знает, где селитра."
    sdme "А если..."
    sh "Не знает!"
    el "Успокойся, Саш. Там у склада Славя дежурит, Шурик там радиодетали нашёл."
    
    stop music fadeout 3
    
    sh "Да, мы, собственно, из-за этого и тут. Поможешь нам?"
    sdme "Не вопрос! Пошли, заодно и Славю предупредим."
    
    if cyber >= 2:
        scene ext_clubs_day 
        play ambience ambience_camp_center_day
        show sh normal pioneer at right
        show el normal pioneer at left 
        with dissolve
        "По пути Шурик спросил меня:"
        sh "Саш, что ты решил насчёт вступления в ряды кибернетиков?"
        
        menu:
        
            "\[Кибернетик\] Согласиться":
            
                if isAssigned == False:
                    $ mulsl += 0.5
                    $ isAssigned = True
                $ cyber = 5
                $ cyber_worker = True
                
                if not persistent.sdAch_cyber:
                    $ persistent.sdAch_cyber = False
                    $ sdGetAch("sdAch_cyber")
        
                sdme "А давай!"
                
                show sh smile pioneer
                
                sh "Отлично! Завтра ждём тебя с утра."
        
            "Отказаться":
        
                sdme "Думаю, я не так уж и хорошо разбираюсь в электронике, так что я пас."
                sh "Ну, как знаешь. Нам пара рук никогда не помешает."
        
    
    scene bg ext_warehouse_day
    play ambience ambience_camp_center_day
    show sh normal pioneer at center
    show el normal pioneer at fleft 
    with dissolve
    
    "У склада было пусто. Моё предчувствие говорило, что Алиса тут уже побывала. Я нервно сглотнул." 
    sdme "Это... Плохо."
    
    show sl shy pioneer at fright with dissolve
    
    if slmet == True:
        "На горизонте показалась Славя. Увидев меня, она порозовела и отвела взгляд."
    else:
        "На горизонте показалась девочка. Приглядевшись, я понял, что это та самая помощница вожатой."
        "Увидев меня, она порозовела и отвела взгляд."
    sl "Ой, привет, ребят. Вы меня ждёте?"
    sdme "Славь, у нас к тебе серьёзный вопрос. В лагере завёлся бунтарь."
    
    show sl serious pioneer with dspr
    
    "Её лицо тут же приняло серьёзное выражение."
    sl "Что такое?"
    sh "Алиса. Она... Не приходила сюда?"
    sl "Э-э-э... Нет. Не уверена. {w}Меня Ульянка попросила помочь с... {w}Ладно, вам лучше не знать."
    sdme "У нас огромные проблемы."
    "Рассказав историю с Алисой, мы вломились на склад."
    
    scene bg int_warehouse_day
    show sl surprise pioneer at fright
    show sh surprise pioneer behind sl at center
    show el surprise pioneer at fleft
    with dissolve
    
    sl "Селитра! Она стояла прямо на этой полке!"
    "Я обречённо сел на пол и закрыл лицо руками."
    sdme "Это я виноват. Я ей дал уйти."
    
    show sl normal pioneer 
    show sh normal pioneer 
    show el normal pioneer
    with dspr
    
    sl "Саш, не вини себя. Мы все хороши. Мне не следовало покидать пост, а ребята не смогли её удержать."
    "Я ничего не ответил."
    sl "Хватит. Сидеть и убиваться тут - не лучшая идея."
    sdme "И что ты предлагаешь?"
    sl "Обдумать всё хорошенько. Может, нам удастся застать её врасплох ещё до того, как она навредит."
    sdme "Тогда соберёмся часов в шесть на площади и составим план."
    el "А ужин во сколько?"
    sh "В семь."
    sdme "Хорошо. Успеем."
    "Мы разошлись, и я отправился на площадь."
    
jump sdMapLoop_1day

#============================== Пирс ================================

label sdUn_1day:
    $ persistent.sprite_time = "day"
    $ day_time()
    $ sd_chapter(1, u"Пирс.")
    scene bg ext_square_day with dissolve
    stop music fadeout 3
    play ambience ambience_camp_center_day
    
    "Я заметил на карте лодочную станцию."
    th "Было бы неплохо сейчас освежиться..."
    "Недолго думая, я двинулся в сторону пристани."
    
    stop ambience
    scene bg ext_boathouse_day with dissolve
    play ambience ambience_boat_station_day
    play music music_list["reflection_on_water"]
    
    "Тёплый воздух обдувал лицо, волны шелестели рядом с берегом..."
    "Я наконец-то почувствовал все прелести лета."
    "Не то, чтобы я сидел целыми днями дома, но и из города часто не выезжал."
    "А если и выезжал, то только на дачу. {w}С картошкой. {w}Которую надо копать."
    "Я подошёл к берегу и обмакнул руку в воде."
    th "Прохладная..."
    th "Видимо, день сегодня не такой уж и жаркий."
    "Я сел на берегу, обхватив колени, и глубоко задумался."
    "Я был рад, что наконец-то выбрался куда-то за город."
    "И тем более в хвалёный Совёнок, который, по мнению отдыхавших в нём, менял судьбы."
    "Я усмехнулся."
    th "Нет, бред какой-то. Из-за какого-то лагеря судьба не изменится."
    th "Да и что вообще есть судьба? {w}Послание свыше? {w}Сценарий, по которому ты должен прожить жизнь?"
    th "Если человек правда так думает, он совсем перестанет что-либо делать."
    th "Только сиди да жди своей участи. Нет, это совсем нелепо."
    "Я наконец оторвал глаза от водной глади и посмотрел на пирс."
    "Там всё это время сидела Лена."
    if unspotted == False:
        "Не знаю, почему я запомнил её."
        "В ней было что-то, что никак не давало мне успокоиться."
    
    "Она, видимо, тоже о чём-то задумалась."
    "Её ноги едва касались воды, сама же девочка отрешённо смотрела вдаль."
    
    stop music fadeout 3
    
    th "Куда она вглядывается?"
    
    if (unspotted == True) and (unmet == False):
    
        th "Нет, в этот раз я всё-таки поговорю с ней! Хватит стесняться."
        
    menu:
    
        "Подойти":
        
            $ knew_about_un_1day = True
            "Я направился на пирс."
            "Встав на первые пару досок, я понял, что его явно делали пионеры."
            "Доски болтались, местами щель была шириной с мою ногу."
            "Невнимательный человек мог тут же провалиться и застрять в этой пристани до приезда следующей смены."
            th "Да, прямо райское место."
            
            show un normal pioneer with dissolve
            
            "Наконец, я добрался до Лены."
            "И тут же пожалел об этом."
            "Доска, на которую я встал, треснула."
            "Всё бы ничего, но..."
            
            show un surprise pioneer with dspr:
                linear 0.5 pos(0.5, 2.0)
            play music music_list["that_s_our_madhouse"] fadein 3
            play sound sfx_water_emerge
            
            "На этой же доске сидела и Лена."
            
            hide un
            $ sdChangeMood(-10)
            
            un "А-а-а-а!"
            sdme "Лена!"
            "Испуганно закричал я."
            sdme "Держись, я сейчас!"
            "Я скинул с себя рубашку и галстук и бросился в воду."
            
            play sound sfx_water_emerge
            stop music fadeout 3
            
            "Подхватив утопающую, я вытащил её на берег."
            "Она откашлялась и села."
            
            if not persistent.sdAch_bridges:
                $ persistent.sdAch_bridges = False
                $ sdGetAch("sdAch_bridges")
            show un normal pioneer with dissolve
            play music music_list["what_do_you_think_of_me"]
            
            un "Спасибо..."
            sdme "За что? Что доску под тобой сломал?"
            sdme "Сам накосячил - сам исправил. Это долг."
            "Я вдруг заметил, что её рубашка порвана в нескольких местах на спине. Из них сочилась кровь."
            sdme "Лен, мне кажется, нам с тобой надо в травмпункт."
            
            show un surprise pioneer with dspr
            
            un "Что такое?"
            sdme "Ты не чувствуешь? Ты всю спину ободрала о доски!"
            un "Наверное, ты прав."
            
            show un smile pioneer with dspr
            
            un "Ещё и вода холодная..."
            "Грустно улыбнулась она."
            
            if unmet == True: 
            
                th "Да, не подумал бы, что эта девочка может обладать самоиронией."
                
            sdme "Зато пляжный сезон открыла!"
            
            show un smile2 pioneer with dspr
            
            un "Да..."
            "Она попыталась подняться на ноги, но тут же свалилась."
            
            show un cry pioneer with dspr
            
            un "Ай!"
            sdme "Что такое?"
            "Вскакивая, спросил я."
            un "Нога..."
            sdme "Подвернула?"
            un "Не знаю... Болит жутко..."
            sdme "Терпи, казак!"
            
            show un cry_smile pioneer with dspr
            
            "Я вдруг заметил ссадину на её ноге."
            sdme "Я сейчас. Только не шевелись и смотри, чтобы песок или что похуже не попало."
            "Я указал ей на рану. Она кивнула и села на песок, прикрыв рану рукой."
            
            hide un with dissolve
            
            "Мне вновь пришлось пройтись по хлипокму пионерскому мостику."
            th "Ну вот почему такое всегда случается именно со мной?"
            th "Будто я - центр притяжения всех неудач на этом свете..."
            "Я вздохнул.{w} Неприятности, бывало, преследовали меня по пятам."
            "Всякий раз, когда я что-то брал в руки, это либо ломалось, либо становилось смертельным инструментом для окружения."
            "Я попытался отогнать эти мысли. {w}Как бы там ни было, надо было действовать."
            "Подобрав свои вещи, я натянул на мокрое тело рубашку и вернулся к Лене уже с галстуком в руках."
            
            show un normal pioneer with dissolve
            
            "От боли она, видимо, слегка отошла."
            sdme "Давай хоть так перевяжем."
            "Я обвязал её ногу галстуком."
            "Лена наблюдала за процессом внимательно, аккуратно помогая с перевязкой."
            
            show un smile pioneer with dspr
            
            "Подняв глаза на неё, я увидел на её лице благодарную улыбку."
            sdme "Готова идти?"
            un "Да..."
            
            show un shy pioneer close at sdCarrying with dissolve
            
            "Крякнув от усталости - все эти занятия спортом явно меня вымотали - я закинул её руку себе на плечо, подхватил на руки и понёс в лагерь."
            "Она явно смутилась, но особо не протестовала."
            "Да и как иначе она бы добралась до медпункта?"
            
            show un normal pioneer close at sdCarrying with dspr
            
            "Через некоторе время Лена спросила:"
            un "Не тяжело?"
            sdme "Нет..."
            "Конечно, мне было тяжело."
            "Но разве мог я признать это рядом с человеком, который нуждается в помощи?"
            "Тем более, если этот человек - приятная девушка."
            un "Я могу пойти, если всё-таки тяжело."
            sdme "Нет, всё в порядке, Лен. Не беспокойся."
            
            stop music fadeout 3
            stop ambience
            play ambience ambience_forest_day
            scene ext_path_day with dissolve
            show un normal pioneer close at sdCarrying
            show sl normal pioneer far 
            with dissolve
            
            "Мы только отошли от пристани, как вдруг навстречу нам вывернула Славя."
            "Шла она с полотенцем в руках, в которое что-то было завёрнуто."
            th "И это -  помощница вожатой?"
            th "В лагере, наверное, сейчас куча работы."
            
            show sl surprise pioneer with dissolve
            
            "Завидев нас, Славя тут же набросилась с вопросами."
            sl "Что, что такое? Что случилось?"
            un "Ничего страшного, просто немного ободралась..."
            "Ответила Лена."
            sdme "У неё на ноге порез, не может идти."
            "Пояснил я."
            
            show sl serious pioneer with dspr
            
            sl "Ох, так это вам в медпункт надо, я вам покажу, где это!"
            "Я кивнул."
            
            $ sdTimeskip()
            scene ext_path_day with sdTransition
            show un normal pioneer close at sdCarrying
            show sl normal pioneer 
            with dissolve
            
            "Мы шли за Славей по вытоптанной тысячами пионерских ног тропинке."
            sl "А что случилось-то?"
            sdme "Доску сломали, Лена на ней сидела."
            
            show sl smile pioneer with dspr
            
            "Пионерка проглотила смешок."
            
            show sl normal pioneer with dspr
            
            sl "Извините. А как ты ногу повредила?"
            un "Сама не знаю. Может, гвоздём задело."
            sdme "В любом случае, тебе надо переодеться в сухое и обработать раны."
            sdme "Первый, блин, день..."
            "Подавленно добавил я."
            sl "А ты что грустный? Ты же девушку спас."
            sdme "Сначала я её уронил."
            
            show sl smile pioneer with dspr
            
            "Славя улыбнулась мне, пытаясь подбодрить."
            sl "В любом случае, ты же не кинул её там тонуть!"
            sdme "А кто-нибудь кинул бы, думаешь?"
            
            show sl serious pioneer with dspr
            
            sl "Ты не знаешь, какие иногда люди бывают..."
            
            show sl normal pioneer with dspr
            
            sl "Лен, ты, главное, не переживай. Всё сейчас сделаем!"
            
            show un smile pioneer close with dspr
            
            un "Да я и не переживаю. Просто болит немного..."
            sdme "Нам долго идти?"
            
            show un normal pioneer close with dspr
            
            sl "Уже почти пришли."
            
            stop ambience
            play ambience ambience_camp_center_day
            scene bg ext_square_day with dissolve
            show un normal pioneer close at sdCarrying
            show sl normal pioneer at right behind un
            with dissolve
            
            "Мы вышли на площадь. {w}Вожатая, завидев нашу компанию издалека, бросила читать свою книгу и в два шага достигла нас."
            
            show mt surprise panama pioneer with dissolve
            
            mt "Ребята, что случилось?"
            sdme "Небольшая травма, ничего страшного. Просто Лене больно наступать."
            
            show mt shocked panama pioneer with dspr
            
            mt "Так, срочно в медпункт! Я сейчаc предупрежу врача, она тебя осмотрит."
            mt "Нам туда. Саш, ты справишься?"
            sdme "Справлюсь, если не будем здесь долго стоять."

            scene bg ext_aidpost_day with dissolve
            show un normal pioneer close at sdCarrying
            show sl normal pioneer at fright behind un
            show mt normal panama pioneer at cleft behind un
            with dissolve
            
            "В медпункт нас сопровождала Ольга Дмитриевна."
            "По пути мы рассказали подробнее, что всё-таки случилось."
            
            if mt_denied_1day:
            
                sdme "Видите, я же говорил, что со мной вечно что-то случается."
                mt "Да брось, Саш. Со всеми что-то случается. Тем более, ты же помогаешь."
                sdme "Да... Но было бы гораздо лучше, если бы повода не было."
                
                show mt smile panama pioneer with dspr
                
                mt "Все так думают, если случается что-то плохое."
                mt "Не переживай. Посмотри на Лену. Она наверняка благодарна тебе, что ты сейчаc о ней заботишься."
                
                show un smile pioneer close at sdCarrying with dspr
                
                "Лена улыбнулась."
                
                show un normal pioneer close at sdCarrying with dspr
                
            
            show cs normal glasses at cright with dissolve
            play music music_list["glimmering_coals"] fadein 3
            
            "У порога нас уже ждала молодая медсестра, немного старше Ольги."
            "Халат сидел на ней, мягко сказать, {w}вызывающе. {w}Правда, обладательница сего наряда ничуть не стеснялась."
            
            show cs shy glasses with dspr
            
            "Поймав мой взгляд в области верхней застёгнутой пуговицы, она хищно подмигнула мне."
            "Мои щёки тут же загорелись. Было ощущение, что она специально носит халат {i}так{/i}."
            
            show cs normal glasses with dspr
            
            csp "И что тут у нас случилось?"
            mt "Ногу повредили."
            
            show cs smile with dspr
            
            csp "Так-так-так... Первый день в лагере, а уже парня подцепила, красавица?"
            
            show un shy pioneer close at sdCarrying with dspr
            
            "Лена, как, впрочем, и я - опять - раскраснелась."
            
            show cs normal with dspr
            
            csp "Ну ладно, проходите, осмотрю вашу рану... Пионеры..."
            "Вздохнула медсестра."
            csp "Ах да... Можете звать меня Виолой."
            
            scene bg int_aidpost_day with dissolve
            stop ambience
            play ambience ambience_medstation_inside_day
            show un normal pioneer close at sdCarrying with dissolve
            
            "Я посадил Лену на кушетку и подошёл к медсестре."
            
            hide un
            show cs normal glasses
            with dissolve
            
            sdme "Ей бы ещё одежду сменить на сухую, простынет ещё."
            "Виола блеснула своими разноцветными глазами. Мне показалось, будто она усмехнулась этому предложению."
            "Будто в этой фразе имелся некий подтекст, но, уверяю, ничего такого я и думать не собирался."
            cs "Это ты верно подметил. Беги, я пока присмотрю за ней."
            
            stop ambience
            play ambience ambience_camp_center_day
            scene bg ext_aidpost_day with dissolve
            stop music fadeout 3
            
            "Я вышел за дверь и перевёл дух."
            "Да, Виола производила впечатление. А учитывая, что я частенько любил оцарапаться или синяк получить..."
            "Я представил, что мне придётся сталкиваться с ней постоянно, и вздрогнул от предвкушения."
            "Немного отдышавшись, я понял, что не знаю, где домик Лены."
            th "Может быть, хоть вспомню, с кем она живёт?"
            "Я брёл к площади, вспоминая, с кем же поселили пионерку, как ответ сам настиг меня."
            
            play sound sfx_body_bump
            
            "Точнее сказать, врезался на полной скорости." with vpunch
            
            show mi surprise pioneer with dissolve
            play music music_list["miku_song_flute"]
            
            mi "Ой, Саш, прости, я такая невнимательная... Ты не ушибся? Может, тебе в медпункт? Как неловко получилось..."
            sdme "Да я, собственно, только оттуда."
            
            show mi scared pioneer with dspr
            
            "Мику поменялась в лице."
            sdme "Не переживай, всё нормально. Слушай, ты с Леной живёшь?"
            
            show mi normal pioneer with dspr
            
            mi "Да, а что, ей что-то нужно? Я могу принести."
            sdme "Принеси, пожалуйста, ей сменную одежду в медпункт. Он промокла, ей переодеться надо."
            
            show mi grin pioneer with dspr
            $ renpy.pause(1)
            hide mi with dissolve
            stop music fadeout 3
            
            "Мику тут же как ветром сдуло."
            "Я усмехнулся и вернулся внутрь."
            
            scene bg int_aidpost_day with dissolve
            stop ambience
            play ambience ambience_medstation_inside_day
            show un normal pioneer at fleft
            show cs normal glasses at cright
            with dissolve
            
            cs "...Ничего ведь?"
            
            show un shy pioneer with dspr
            
            "Увидев меня в дверях, Лена замялась и покраснела. Видимо, вопрос шёл о её личной жизни."
            "Виола повернулась ко мне, в отличие от Лены, ничуть не смутившись."
            cs "Уже?"
            sdme "Соседку Лены встретил. Сейчас она принесёт вещи."
            
            show un normal pioneer with dspr
            
            "Виола кивнула и жестом пригласила сесть."
            cs "Ну, пионер, что думаешь теперь делать?"
            "Я задумался."
            
            menu:
            
                "Присмотреть за Леной":
                
                    $ sdMap_1day += 1
                    $ unneedaid = True
                    $ mood = 100
                    $ ptsun += mulun * 2
                    $ medic += 2
                    $ mtmerc += 1
                    $ impression += 10
                    
                    th "Уйти всё-таки будет не по-людски, наверное."
                    th "Да и всё равно меня пока никто не ищет."
                    sdme "Я, пожалуй, останусь."
                    cs "Вот и отлично! А мне надо... По делам убежать."
                    
                    hide cs with dissolve
                    show un normal pioneer at center with ease
                    
                    "И она растворилась в воздухе, оставив нас с Леной наедине." 
                    
                    jump sdUn_1day_aidpost
                    
                "Уйти":
                
                    th "Доставить я её доставил, медсестра, в принципе, справится сама."
                    sdme "Я пойду. В медицине я плохо разбираюсь, так что вряд ли из меня выйдет толковый помощник."
                    cs "Ну ладно, иди."
                    
                    scene bg ext_aidpost_day with dissolve
                    
                    "Я вышел из медпункта и задумался."
                    th "Куда теперь?"
                    "Немного поразмыслив, я решил вернуться к площади."
                    
        "Окликнуть отсюда":
        
            sdme "Лена!"
            "Крикнул я и замахал рукой, как ненормальный."
            th "Да, на её месте я бы точно испугался и убежал."
            th "Хотя, куда ей тут бежать? Только если по воде."
            "Я сильно сомневался, что Лена обладает такими навыками, поэтому терпеливо ждал её на берегу."
            
            show un shy pioneer with dissolve
            play music music_list["raindrops"] fadein 5
            
            un "Привет..."
            "Она и правда выглядела испуганной."
            "Ну, или по крайней мере удивлённой."
            th "Теперь надо как-то перед ней оправдаться."
            sdme "Я тут сидел и... Увидел тебя."
            
            show un normal pioneer with dspr
            
            "Язык начал заплетаться, дрожь в коленках давала знать, что зря я это затеял."
            sdme "Ты... Так одиноко выглядела там... Я подумал, может..."
            th "Да что с тобой, Александр?! Возьми себя в руки!"
            "Я набрал грудью воздух и на выдохе сказал:"
            sdme "В общем, может, тебе захочется с кем-нибудь поговорить?"
            "Стало немного легче."
            
            show un smile2 pioneer with dspr
            
            un "Я не против."
            
            show un normal pioneer with dspr
            
            un "И что же ты хочешь узнать?"
            "Я задумался."
            th "А ведь действительно, о чём мне с ней говорить?"
            "Я вспомнил об одной вещи."
            sdme "Ты так пристально смотрела куда-то на том острове... Будто высматривала что-то."
            "Я махнул рукой в сторону острова."
            un "А-а-а, это..."
            
            show un normal pioneer with dspr:
                center
                sdSitDown
            
            "Она села на песок, обняв себя за колени."
            "Видимо, она о чём-то вспомнила, потому что в её взгляде я заметил тоску."
            un "Просто..."
            
            show un normal pioneer close at sdSitCenter with dspr
            
            "Я плюхнулся рядом."
            un "Просто, я тут не в первый раз."
            "Продолжила Лена."
            un "А на том острове есть одно красивое место..."
            sdme "Природу любишь?"
            "Я улыбнулся."
            
            show un smile2 pioneer close with dspr
            
            un "Да, пожалуй..."
            sdme "Я тоже, наверное."
            
            show un surprise pioneer close with dspr
            
            un "Как это - наверное?"
            sdme "Не знаю точно. Не придавал этому какое-то особое значение."
            
            show un normal pioneer close with dspr
            
            un "Ну... Рано или поздно узнаешь."
            
            show blinking
            
            "Мы сидели рядом и смотрели на водную гладь."
            "В итоге я решил прервать это молчание."
            sdme "И как тебе Совёнок?"
            
            show un smile3 pioneer close with dspr
            
            un "Тут красиво. И нескучно."
            sdme "А знакомые в смене есть?"
            
            show un normal pioneer close with dspr
            
            un "Нет... Из нашего отряда точно никого не знаю."
            sdme "Новые знакомства - всегда хорошо."
            un "Да, наверное..."
            "В её голосе заиграли нотки грусти."
            
            if unmet == True:
            
                "Мне захотелось её как-то успокоить."
                "Сказать что-нибудь приятное, дружелюбное..."
                sdme "Не волнуйся, ты скоро привыкнешь."
                
                show un surprise pioneer close with dspr
                
                un "К чему?"
                sdme "К смене обстановки. Такое у всех бывает."
                
                show un normal pioneer close with dspr
                
                un "Бывает... Но у меня это всегда."
                sdme "Ничего страшного."
                sdme "Знаешь, есть поговорка. «Всё пройдёт». Вот и это тоже пройдёт."

                show un smile pioneer close with dspr

                "Я дружелюбно улыбнулся."
                un "Может, ты и прав."           
            
            show un normal pioneer at sdGetUp with dspr
            
            "Лена, видимо, куда-то собиралась."
            sdme "Уже возвращаешься?"
            un "Да, я хотела искупаться, но вода... Холодная до жути."
            th "Странно... Искупаться хотела, а с собой даже полотенца не взяла."
            "Я тоже поднялся на ноги."
            un "Ты тоже пойдёшь?"
            sdme "Да, пожалуй. Хочу немного освоиться в лагере."
            un "Тогда пошли."
            "Я был немного удивлён тем, что Лена сама предложила пройтись, но отказываться не стал."
            
            stop music fadeout 3
            
            sdme "Пошли."
            
            stop ambience
            play ambience ambience_forest_day
            play music music_list["smooth_machine"] fadein 3
            scene bg ext_path_day with dissolve
            show un normal pioneer with dissolve
            
            "Дорога до пристани шла сквозь небольшой лесок."
            "По пути сюда я этого и не заметил, но теперь эта деталь казалась мне очень важной."
            "Только сейчас я заметил, что вокруг действительно живописная природа."
            sdme "Красиво..."
            
            show un smile2 pioneer with dissolve
            
            un "Да..."
            "Мечтательно протянула Лена."
            "Я взглянул на неё."
            "Похоже, она действительно была рада побывать на природе."
            "Остальной путь мы проделали молча, проплывая мимо деревьев и созерцая всю красоту этого леска."
            "Кроны деревьев медленно покачивались, смыкаясь и размыкаясь над нашими головами."
            "Свежий воздух природы расслаблял и давал трезвость мысли."
            "Это действительно было прекрасно. {w}В этот момент я понял, что точно не буду жалеть об этой поездке."
            
            stop music fadeout 3
            stop ambience
            play ambience ambience_camp_center_day
            scene bg ext_square_day with dissolve
            show un smile pioneer with dissolve
            
            un "Ну вот, мы и пришли. Я побегу располагаться, встретимся во время обеда!"
            
            hide un with dissolve
            
            "И она тут же исчезла. {w}Я лишь успел кивнуть, да и не был уверен, что она это заметила."
            
    $ unmet = True
            
    $ sdDisableCurrentZone()
    
jump sdMapLoop_1day

label sdUn_1day_aidpost:

    show blink with dissolve
    $ renpy.pause(1)
    $ persistent.sprite_time = "day"
    $ sd_chapter(1, u"Медпункт.")
    $ day_time()
    scene int_aidpost_day
    show un normal pioneer
    hide blink
    show unblink
    
    "Думая, о чём завести разговор, я вспомнил, что Лена до своего падения куда-то всматривалась." 
    "Мне захотелось разузнать, что же так пленило её внимание."
    sdme "Лен, можно узнать у тебя кое-что?"
    
    show un shy pioneer with dspr
    
    "Лена вопросительно посмотрела на меня." 
    sdme "Там, на пристани, ты будто была отрешена от всего, раз не заметила, как я подошёл. Что так заняло тебя?"
    
    show un normal pioneer with dspr
    
    un "Ну... Просто, я уже тут была. Вспомнила о прекрасном месте на одном из островов."
    sdme "Природу любишь?"
    un "Ну... Да. А ты?"
    sdme "Не задумывался. Наверное, да."
    un "А чем ты занимаешься?"
    sdme "В основном с друзьями гуляю. Поигрываю для себя на гитаре..."
    
    show un smile2 pioneer with dspr
    
    "Глаза у Лены загорелись."
    un "А что ты играешь?"
    sdme "Разное... В основном, переигрываю рок-песни."
    
    show un surprise pioneer with dspr
    
    un "Они же запрещены!"
    sdme "Да брось. Через несколько лет наши дети будут свободно слушать подобные жанры."
    unk "И я так думаю."
    "Cказал кто-то прямо над моим ухом. Я подскочил на месте." with vpunch
    
    show un grin pioneer at right with ease
    show mi grin pioneer at left with dissolve
    play music music_list["so_good_to_be_careless"]
    
    "За мной стояла Мику."
    sdme "Не делай так больше, пожалуйста."
    
    show un normal pioneer with dspr
    
    mi "Извини. Я принесла, Лен."
    
    show mi smile pioneer with dspr
    
    "Она протянула одежду соседке."
    
    show un smile pioneer with dspr
    
    un "Спасибо."
    
    stop ambience fadeout 3
    scene ext_aidpost_day
    show mi normal pioneer 
    with dissolve
    play ambience ambience_camp_center_day
    
    "Мы с Мику, не задумываясь, вышли." 
    mi "Саш, расскажешь, что с ней?"
    sdme "Это моя вина. Она на пирсе сидела, а я подошёл к ней, и доски сломались."
    
    show mi smile pioneer with dspr
    
    mi "Ну и где же тут твоя вина? "
    sdme "То есть? Я встал на непрочную доску..."
    mi "Она бы рано или поздно всё равно сломалась. А ты спас человека, который мог пострадать из-за этого. Нет тут твоей вины."
    "Я почесал затылок. "
    sdme "Возможно, ты права."
    
    show mi grin pioneer with dspr
    
    mi "Ну вот! Не унывай!"
    
    stop music fadeout 2
    
    un "Заходите."
    
    stop ambience
    scene int_aidpost_day
    show mi normal pioneer at sdSitLeft
    show un normal sport at sdSitRight
    with dissolve
    play music music_list["take_me_beautifully"] fadein 2
    
    "Лена уже сложила в стопку свои мокрые вещи и положила их на койку. Мы разместились рядом, образовав круг."
    
    show mi smile pioneer with dspr
    
    mi "А давайте сыграем в игру!"
    "Мику достала из кармана небольшой попрыгунчик."
    mi "К кому попадёт эта штука, тот говорит один факт о себе."
    "Мы кивнули."
    
    show mi normal pioneer with dspr
    
    mi "Итак, начнём с..."
    "Она подкинула мячик, и я инстинктивно поймал его."
    sdme "Моя реакция меня погубят... Эм-м-м... Ну... Я поигрываю на гитаре."
    
    show un shy sport with dspr
    
    un "Об этом я знаю."
    
    show mi grin pioneer with dspr
    
    mi "Давай новую информацию."
    sdme "Ладно, секунду..."
    th "А чем я ещё занимаюсь?"
    
    show un normal sport
    show mi normal pioneer 
    with dspr
    
    sdme "Я мастерю всякие штуки с Электроником."
    th "Не то, чтобы мы собирали что-то невероятно крутое, но парочку радиоприёмников собрать могли."
    mi "Ладно..."
    "Мику забрала у меня мячик."  
    mi "Моя очередь."
    mi "Я много занимаюсь музыкой. {w}Ну прям очень много. "
    mi "В Японии у нас дома был целый оркестр, и я каждый день бралась за новый инструмент. {w}А когда мы решили переехать, мне пришлось выбрать самые любимые, и я часа четыре не могла определиться."
    sdme "И что же в итоге выбрала?"
    
    show mi smile pioneer with dspr
    
    mi "Гитару, бонги и пианино. {w}Не знаю, почему. {w}Странно, да?"
    sdme "Вообще-то не так уж и странно. Боб Марли примерно такой же выбор сделал."
    
    show mi normal pioneer with dspr
    
    mi "Да, пожалуй, ты прав. Идём дальше."
    "Мику передала мячик Лене."
    un "Я... Люблю природу. Наверное, это у меня от отца."
    mi "И чем же он занимается?"
    
    show un grin sport with dspr
    
    un "А это я расскажу, когда очередь опять дойдёт до меня."
    sdme "Ах ты хитрюга! Ну ладно, хорошо. Я... Мне приходится часто присматривать за сестрой."
    
    show mi surprise pioneer with dspr
    show un sad sport with dspr
    
    mi "И что в этом такого? Мне, вот, тоже приходится."
    
    show mi upset pioneer with dspr
    
    extend " Она такая... {w}Безбашенная и бездумная."
    
    show mi normal pioneer 
    show un normal sport
    with dspr
    
    mi "Мы, конечно, в хороших отношениях, но иногда... Бывает."
    sdme "Бывает. Но она всё-таки твоя сестра."
    "Мику кивнула."
    mi "Идём дальше. Я порой бываю слишком легкомысленной. Поэтому важные вещи я обдумываю очень долго и тщательно, и в эти моменты не похожа сама на себя."
   
    show un smile sport with dspr

    un "Зато в итоге ты примешь правильное решение."
    
    show mi grin pioneer with dspr
    
    mi "Да, возможно. Но пока, к счастью, от этого ничего не зависело."
    "Лена взяла попрыгунчик."
    
    show mi normal pioneer with dspr
    
    un "Мой отец - лесник. Он часто живёт... Жил в хижине. И иногда он брал меня с собой."
    
    show un cry_smile sport with dspr
    
    un "Мы целыми днями гуляли по лесу, а вечерами сидели в хижине и слушали звуки природы. И здесь так же..."
    "Она будто отрешилась от нас на какое-то время."
    th "Теперь понятно, почему она такая молчаливая. Это место ей напоминает об отце."
    
    show un cry sport at sdSitRight with dissolve
    
    "Лена повернулась к нам, на её глазах проступили слёзы."
    un "Извините... Воспоминания..."
    "Мы с Мику синхронно положили руки ей на плечи."
    mi "Всё хорошо."
    
    show un cry_smile sport with dspr
    
    un "Спасибо."
    
    show un normal sport with dspr
    
    "Прошло ещё немного времени, пока Лена успокоилась, и мы продолжили."
    sdme "Я ещё ни разу не был в лагерях."
    
    show mi grin pioneer with dspr
    
    "Мику перехватила попрыгунчик."
    mi "Я тоже."
    
    show un grin sport with dspr
    
    un "Тогда вам многое предстоит узнать."
    
    show mi surprise pioneer with dspr
    
    "Мику протянула ей мячик."
    mi "Рассказывай."
    
    show un normal sport
    show mi normal pioneer 
    with dspr
    
    un "Ну... Я была тут в прошлом году. Познакомилась со Славей. И она тогда очень много помогала Ольге Дмитриевне."
    sdme "Теперь понятно, почему она сразу помощницей вожатой стала."
    un "Да, ей нравится это дело. Но Ольга Дмитриевна - такой человек...{w} Ей важно, чтобы все были примерными пионерами и работали на благо партии. Так что работать нам придётся часто."
    "Я вздохнул."
    sdme "Лагерь вроде для отдыха предназначен, разве нет?"
    
    show un grin sport with dspr
    
    un "Размечтался!"
    mi "А ты чем занималась здесь?"
    
    show un normal sport with dspr
    
    un "Обычно тут сидела, помогала медсестре. А в этот раз вот как получилось..."
    sdme "Не переживай, завтра-послезавтра уже на ноги встанешь. Это я тебе точно говорю, я не раз такие раны получал."
    
    show un surprise sport
    show mi surprise pioneer 
    with dspr
    
    mi "Это где же?"
    sdme "По разным стройкам гуляли. Арматуры - лучшие друзья."
    "Я немного закатал штанину и показал свои швы."
    mi "Ой, Саш... Аккуратнее же надо быть."
    sdme "Как могу."
    "Я ещё долго рассказывал, как мы бродили в лесах, как часто приходилось падать, как часто стиралась кожа о гравий после падения с велосипеда, подтверждая всё шрамами и швами на теле. "
    un "Как ты вообще ещё жив?"
    sdme "Чудеса. Видимо, кому-то нужно, чтобы я был жив."
    
    show mi scared pioneer at sdGetUp with dspr
    show un shy sport with dspr
    
    "Мику вдруг вскочила."   
    mi "Ох, ребят, засиделась я тут с вами! Мне же бежать надо, до встречи!"
    
    stop music fadeout 3
    play ambience ambience_medstation_inside_day
    hide mi with easeoutleft
    show un surprise sport at sdSitCenter with ease
    
    "И она убежала. Мы с Леной непонимающе переглянулись."
    
    show un shy sport with dspr
    
    un "Что это такое было?"
    sdme "Вот и я не понял. Зато будет чем развлечься."
    
    show un smile2 sport with dspr
    
    "Я показал ей попрыгунчик и начал кидать его в стену."
    "Лена перехватила его и продолжила моё занятие."
    sdme "А ты какую музыку слушаешь?"
    
    show un normal sport with dspr
    
    un "Просто красивые песни."
    sdme "Какие именно?"
    un "Ну... «Still loving you», к примеру."
    sdme "Так это же запрещённые Scorpions!"
    
    show un smile sport with dspr
    
    "Лена улыбнулась."
    un "Ну... Да... Но песня-то хорошая!"
    sdme "И я тебе о том же, у них все песни такие."
    
    hide un with dissolve
    
    "Хоть в чём-то мы были похожи. Но всё-таки молчаливая и спокойная Лена, любившая посидеть в прострации, слишком сильно отличалась от меня."
    "Казалось, я ни за что не уживусь с таким человеком."
    "Но почему сейчас я весь день провожу с ней? {w}Просто так, чувствуя свою вину? Или, может, из-за стремления помочь?"
    "Или что-то в ней {i}привлекало{/i} меня?"
    "За всеми этими раздумьями я не заметил, как Лена уснула."
    
    stop ambience fadeout 3
    $ persistent.sprite_time = "sunset"
    $ sunset_time()
    scene bg ext_aidpost_sunset with dissolve
    play ambience ambience_camp_center_day
    
    "Выйдя на улицу, я обнаружил, что солнце постепенно стало клониться к горизонту."
    "Тени уже протягивали ко мне свои лапы, норовясь утащить в царство вечного мрака."
    "В небе розовым оттенком покрывались облака, будто показывая всю свою легкомысленность."
    "Я с удовольствием вдохнул свежий, ещё не испроченный выхлопами воздух."
    th "Пора всё-таки разместиться. Женя, наверное, заждалась."
    "Недолго думая, я закрыл медпункт на ключ и пошёл к сестре."
    
    scene bg ext_square_sunset
    show mz bukal glasses pioneer 
    with dissolve
    
    "Она сидела на лавке и, видимо, ждала меня."
    mz "Ты где ходишь?"
    sdme "Жень, у меня тут форс-мажор. Я человека в воду уронил, теперь в медпункте присматриваю."
    
    show mz normal glasses pioneer with dspr
    
    mz "А-а-а, тогда ладно."
    sdme "Она уснула сейчас, вот я и пришёл помочь."
    
    show mz smile glasses pioneer with dspr
    
    mz "Да я и сама справлюсь."
    sdme "Всё равно пошли, хоть посмотрю, где ночевать придётся."
    
jump sdHouse_1day


label sdUn_Supper_1day:

    show blink with dissolve
    $ renpy.pause(1)
    $ persistent.sprite_time = "sunset"
    $ sd_chapter(1, u"Медпункт. Вечер.")
    $ sunset_time()
    scene int_aidpost_day    
    show un normal sport at sdSitCenter  
    stop ambience
    play ambience ambience_medstation_inside_day
    hide blink
    show unblink
    
    "Когда я вернулся, Лена уже проснулась. Она сидела на койке, потирая сонные глаза и не понимая, что происходит."
    sdme "А, только встала?"
    un "Да..."
    sdme "Извини, я бросил тут тебя."
    
    show un smile sport with dspr
    
    un "Ничего страшного. Никто меня не украдёт."
    "Она заспанно усмехнулась."
    sdme "Как нога?"
    
    show un normal sport at sdGetUp with dspr
    
    "Пионерка попыталась слезть с кровати. Я подошёл к ней, готовясь подстраховать её." 
    "Правда, этого не потребовалось - Лена уже без всяких трудностей стояла на ногах."
    un "Прошла вроде."
    sdme "Это хорошо. Но тебе не стоит сильно напрягаться, лучше дождёмся тётю Виолу, она точно скажет, что делать."
    un "Думаю, ты прав."
    un "Саш, я всё время забываю сказать тебе."
    
    show un smile3 sport with dspr
    
    un "Спасибо. Я бы, наверное, утонула без твоей помощи."
    sdme "Да брось. Любой на моём месте поступил бы так же."
    
    show un grin sport with dspr
    
    un "Но не каждый бы потом донёс до медпункта и сидел столько времени."
    sdme "Я просто хочу помочь."
    
    show un cry_smile sport with dspr
    
    un "Ты очень самоотверженный. Спасибо ещё раз."
    sdme "Не стоит, Лен. Не суди о человеке по одному поступку."
    
    show un smile sport with dspr
    
    un "А по чему ещё надо судить людей?"
    "Я оставил этот вопрос без ответа, молча смотря ей в глаза."
    "Через минуту я опомнился и прервал неловкую паузу."
    sdme "Может, я лучше за едой схожу?"
    un "Это было бы весьма кстати."
    
    stop ambience
    show bg int_dining_hall_people_sunset with dissolve
    play ambience ambience_dining_hall_full fadein 2
    
    "Оказавшись в столовой, я подошёл к окну выдачи и, расписав всю ситуацию, получил двойную порцию."
    unk "Эй! А мне двойную?"
    "Послышалось сзади."
    na "Ульян, тебе и половины много будет с твоим поведением!" 
    "Осадила её тётя Надя. {w}По крайней мере, так назвали повариху несколько ребят передо мной."
    unk "Худей, мелкая!"
    
    show us angry pioneer at right
    show dv grin pioneer2 at left
    with dissolve
    
    "Я оглянулся и увидел двух рыжих пионерок. Та, что была поменьше, судя по всему, Ульяна, тихо свирепела."
    $ usmet = True
    us "Это ты ноешь, что лишние полкило набрала!"
    
    show dv surprise pioneer2
    show us grin pioneer 
    with dspr
    
    dvp "Тихо ты! Теперь полстоловой знает!"
    "Я усмехнулся."
    
    show dv angry pioneer2 with dspr
    
    dvp "А ты что ржёшь, придурок? Смешно тебе?"
    sdme "Есть немного." 
    "Её подруга довольно ухмыльнулась."
    us "Ох, сейчас что-то будет... Алис, только столовую кровью не забрызгай!"
    
    show mi dontlike pioneer with dissolve
    
    "Передо мной выросла Мику."
    mi "Алиса, стой. Человек за больным ухаживает!"
    
    $ dvmet = True
    
    dv "И что, теперь нельзя его проучить?"
    mi "Не в этот раз."
    "Мику строго посмотрела на неё, и Алиса тут же умолкла."
    
    hide dv
    hide us
    with dissolve
    show mi normal pioneer with dspr
    
    "Я двинулся к выходу, моя спасительница последовала за мной."
    mi "Слушай, Саш... Алиса очень вспыльчива, будь с ней аккуратней."
    sdme "Спасибо за заботу, Мику, но я думаю, что справился бы."
    
    show mi smile pioneer with dspr
    
    mi "Только не донёс бы ужин Лене. А это ей сейчас нужно, так что иди."
    "Она махнула мне рукой, и я выдвинулся в медпункт."
    
    stop ambience
    play ambience ambience_medstation_inside_day
    scene bg int_aidpost_day 
    show un normal sport at sdSitCenter
    with dissolve
    
    "Лена всё так же сидела и потирала ногу."
    sdme "А вот и ужин!"
    "Захлопывая дверь левой пяткой, воскликнул я."
    
    show un smile sport with dspr
    
    "Пострадавшая повеселела и быстро разложила трапезу на стол. Через пару минут с едой было покончено."
    "Не сказать, что это было самое вкусное, что я ел в своей жизни, но местные повара явно знали своё дело."
    "Лена, очевидно тоже оценила это и теперь лежала на койке, получая наслаждение от жизни."
    
    show un grin sport with dspr
    
    un "Хорошо тут, однако..."
    "Она втянула свежий воздух из открытого окна и улыбнулась."
    
    show un smile2 sport with dspr
    
    un "Пошли на крыльце посидим!"
    
    #CG бы
    stop ambience
    play ambience ambience_camp_center_evening
    scene bg ext_aidpost_sunset
    show un normal sport at sdSitLeft
    with dissolve
    
    "Мы сидели и смотрели на заходящее солнце. Я окончательно запутался в ней, ведь с каждой минутой она менялась всё больше и больше."
    th "Получается, я с ней провёл весь день..."
    "Лена вдруг помрачнела."
    
    show un sad sport with dspr
    
    un "Саш... Знаешь, ты не обязан присматривать за мной."
    sdme "Ты к чему?"
    
    show un normal sport with dspr
    
    un "Просто... Если ты делаешь это из-за того, что считаешь, что должен, то лучше не надо."
    sdme "Я не считаю так. Я просто хочу узнать тебя получше."
        
    show un surprise sport with dspr
    
    "Лена непонимающе посмотрела на меня."
    un "Зачем?"
    sdme "Не знаю... {w}Мне всегда было интересно узнавать человека, находить общие интересы, общаться, в конце концов."
    
    show un normal sport with dspr
    
    un "Боюсь, у нас не будет общих интересов." 
    sdme "Почему?"
    un "Понимаешь, я ничем не интересуюсь. Единственное, что я делаю - читаю книжки днями напролёт."
    sdme "И почему это плохо?"
    
    show un sad sport with dspr
    
    un "Ну как же! Я не занимаюсь ничем, ничего не умею, ни с кем не..."
    sdme "Хватит. Я тебя понял." 
    "Я замолчал."
    "Стало даже обидно. Обидно за Лену, что она так и оставалась всю жизнь одна."
    "И нет же в ней ничего отвратительного, ничего отталкивающего..."
    "Я решил не наступать на мозоль и сменить тему."
    sdme "Расскажи лучше, что тут было в прошлом году."
    
    show un normal sport with dspr
    
    un "Ну... Я толком ничего и не помню, кроме того, что помогала тёте Виоле и Славе. "
    un "Это место для меня уже как родное." 
    sdme "И что именно ты делала?"
    
    show un grin sport with dspr
    
    un "В основном бинтовала и йодом обрабатывала. А всё остальное время мы сидели и пили чай."
    un "Тётя Виола много чего рассказывала про медицину... А я всегда любила ухаживать за кем-нибудь."
    sdme "Ну вот, а говоришь, нет интересов."
    "Лена задумалась и улыбнулась."
    
    show un smile2 sport with dspr
    
    un "Ты прав, пожалуй. Я переборщила."
    
    show un normal sport with dspr
    show cs smile with dissolve
    
    "На дорожке появилась медсестра."
    
    play music music_list["glimmering_coals"] fadein 5
    
    cs "Ну что, молодёжь, как вы тут?"
    sdme "Всё в порядке, спасибо."
    
    show cs normal with dspr
    
    cs "Как нога, Лен?"    
    un "Прошла, вроде как. Думаю, смогу уже ходить."
    cs "Не торопись. Сейчас тебя осмотрю и там уже решим, что с тобой делать. "
    cs "А ты, пионер..."
    sdme "Саша."
    cs "Саша, задержись на минутку."
    
    hide un with dissolve
    
    "Лена зашла в медпункт, и теперь, наедине с Виолой, мне стало не по себе."
    th "А вдруг она со мной что-нибудь сделает?"
    th "Я же не смогу сопротивляться! Да никто и не поверит!"
    "Я проглотил комок, образовавшийся в горле."
    cs "Справился?"
    sdme "Да, это не так сложно."
    
    show cs close shy with dspr
    
    "Виола сделала пару шагов ко мне. Теперь мои глаза упирались прямо в её разрез."
    th "Ну вот! Началось!"
    cs "Пионер... А ты не хотел бы помочь медпункту?"
    "С паузами, мурлыкающим голосом проговорила она."
    
    stop music fadeout 3
    
    menu:
        "\[Медик\] Согласиться":
        
            if isAssigned == False:
                $ mulsl += 0.5
                $ isAssigned = True
            $ medic = 5
            $ medic_worker = True
            $ mulun += 0.5
            if not persistent.sdAch_medic:
                $ persistent.sdAch_medic = False
                $ sdGetAch("sdAch_medic")
                        
            sdme "Ну... Я знаю пару названий лекарств... Может, и помогу чем."
            cs "Хорошо... Очень хорошо..."
            "Виола улыбнулась и сделала шаг назад."
            cs "Завтра придёшь с утра, до завтрака. Мы с Леной тебя... Посвятим в курс дела."
            "Я поёжился. Мне кажется, или она специально в каждое слово вкладывает скрытый смысл?"
        
        "Отказаться":
            th "Вернуться сюда? {i}Добровольно? {w}К НЕЙ?!{/i}"
            th "Только через мой труп!"
            sdme "Извините, я... Не смогу помочь. Я плохо разбираюсь... В медицине."
            cs "Что ж... Очень жаль."
            "Виола одёрнула халат и отошла от меня."
    
    show cs smile with dspr
    
    sdme "Тогда я... Пойду?"
    cs "Ещё увидимся, пионер."
    "Я кивнул и отправился в свой домик."
    
jump sdComingSoonBranch
    
label sdDiningHelp:
    
    show blink with dissolve
    $ renpy.pause(1)
    $ persistent.sprite_time = "sunset"
    $ sd_chapter(1, u"Помощь в столовой.")
    $ sunset_time()
    scene bg int_dining_hall_people_sunset
    hide blink
    show unblink
    
    "Тётя Надя уже ждала меня."
    na "Вставай на раздачу."
    na "Наливаешь каждому стакан киселя."
    na "В тарелку кладёшь по половнику пюре."
    sdme "Половнику? Разве её не накладывают... Не знаю, чем-нибудь другим?"
    na "Так быстрее, да и дозировать проще. Вообще, молчи и делай своё дело."
    sdme "Ладно..."
    na "Да, и котлетку не забудь. И десерт - вафли."
    th "Если это - котлета, то я - Пётр Первый."
    "Еда выглядела и пахла аппетитно, но количество..."
    th "Если человек действительно голоден, он даже не заметит, что ел."
    th "Может, кому-нибудь стоит положить побольше... Но кому?"
    
    menu:
        "Славе" if slmet:
        
            $ ptssl += 1 * mulsl
    
        "Ульянке" if usmet:
        
            $ ptsus += 1 * mulus

        "Алисе" if dvmet:
            
            $ ptsdv += 1 * muldv
            
        "Лене" if unmet:

            $ dinner_for_un_1day = True

        "Мику":
        
            $ ptsmi += 1 * mulmi
            
        "Никому":
        
            pass
    
    if not knew_about_un_1day:
        "Пока я раскладывал еду, я так и не заметил Лену."
        "Подумалось, что с ней могло что-нибудь случиться."
        "В этот момент подошла Мику."
        
        show mi smile pioneer with dissolve
        
        mi "Ой, привет, Саш! Уже устроился в столовую? Ой, как здорово! Я тоже хочу что-нибудь поделать."
        
        if not music_worker:
            mi "Вот, даже концерт придумала в честь приезда."
            sdme "Ого, это будет интересно."
            mi "Да, мы постараемся! А ты к нам тоже заглядывай, глядишь, может, втянешься."
            sdme "Обязательно загляну."
            "Сказал я, скорее надеясь на то, что она не вспомнит об этом."
            
        sdme "Слушай, Мику, а ты, случайно, не знаешь, где Лена?"
        
        show mi sad pioneer with dspr
        
        mi "Лена... В медпункте. С пирса свалилась."
        sdme "О, ужас!"
        
        show mi normal pioneer with dspr
        
        mi "Не переживай, с ней всё в порядке. Просто медсестра оставила её на всякий случай."
        sdme "Ладно, надеюсь, ты права."
        
    else:
        
        th "А Лена сейчас в больнице... И я даже не знаю, что с ней."
        th "Не стоило, пожалуй, уходить."
        "В этот момент подошла Мику."
        
        show mi smile pioneer with dissolve
        
        mi "Ой, привет, Саш! Уже устроился в столовую? Ой, как здорово! Я тоже хочу что-нибудь поделать."
        
        if not music_worker:
            mi "Вот, даже концерт придумала в честь приезда."
            sdme "Ого, это будет интересно."
            mi "Да, мы постараемся! А ты к нам тоже заглядывай, глядишь, может, втянешься."
            sdme "Обязательно загляну."
            
        sdme "Слушай, Мику, а ты занесла одежду Лене?"
        
        show mi normal pioneer with dspr
        
        mi "Да, она вроде в порядке. Просто медсестра оставила её на всякий случай."
        sdme "Ладно, надеюсь, всё будет хорошо."
        
    show mi smile pioneer with dspr
        
    mi "И я так думаю."
    
    if dinner_for_un_1day:
    
        sdme "Может, ей хоть ужин отнести?"
        mi "Неплохая идея. Давай, я передам."
        sdme "Передай ещё, чтобы поправлялась."
        mi "Хорошо."
        
        $ ptsun += 1 * mulun
    
    hide mi with dissolve
    
    "Она улыбнулась мне, забрала свою тарелку и ушла."
    
    show mz normal pioneer with dissolve
    
    "Через некоторое время появилась Женя."
    sdme "Займёшь мне место?"
    mz "Что за странный вопрос? Конечно."
    
    hide mz with dissolve
    
    "Наконец, поток голодных пионеров закончился."
    na "Молодец, со всем справился. Можешь идти. Завтра ждём тебя перед завтраком."
    sdme "Хорошо."
    "Я направился к Жене."
    
    
    if dv_deed_1day == -1:
        jump sdDiningCyber
        
    show mz normal glasses pioneer at sdSitLeft
    show sl normal pioneer at sdSitCenter
    with dissolve
    
    "Она сидела вместе со Славей."
    mz "Привет. Ты как раз вовремя."
    sdme "Что такое?"
    mz "У нас со Славей возник спор. Электроник в прошлом году ездил сюда?"
    sdme "Да."
    mz "Я же говорила!"
    sl "Я вот не помню его. Не видела."
    "Славя задумалась."
    
jump sdDiningStandard

label sdDiningCyber:
    
    $ renpy.pause(1)
    $ persistent.sprite_time = "sunset"
    $ sd_chapter(1, u"Столовая. Проблемы.")
    $ sunset_time()

    th "Я же должен был остановить Алису!"
    
    if not dining_worker:
    
        "Я со всех ног побежал к столовой."
        scene bg int_dining_hall_people_sunset
        show sl surprise pioneer at sdSitCenter
        with dissolve
        "К счастью, Славя была тут."
    
    else:
    
        "За одним из столиков я увидел Славю и ринулся к ней."
        show sl surprise pioneer at sdSitCenter
        with dissolve
        
    sl "Саш, ты чего?"
    sdme "Извини, я забылся и пропустил всё. Вы что-нибудь придумали?"
    
    show sl sad pioneer with dspr
    
    sl "Я никого не нашла."
    sdme "И... Тогда какой у нас план?"
    sl "У нас...{w} Нет плана. Просто ждём."
    sdme "Но нам надо что-то..."
    el "Её нигде нет."
    
    show el normal pioneer at left behind sl
    show sh normal pioneer at right behind sl
    with dissolve
    
    sdme "Тогда пошли, найдём её!"
    
    hide sl
    hide el
    hide sh
    with dissolve
    
    jump sdEscapeDining_1day
    
#============================== Вечер ===============================
#=============================== ... ================================


